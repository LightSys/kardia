$Version=2$
post_cd_batch "widget/component-decl"
    {
    // Unpost a CD batch - process
    Unpost "widget/component-decl-action" { }
    Unposted "widget/component-decl-event" { }
    unpost_cn "widget/connector"
	{
	event=Unpost;
	target=unpost_osrc_1;
	action=QueryParam;
	a_batch_number = runclient(:a_batch_number);
	a_ledger_number = runclient(:a_ledger_number);
	a_period = runclient(:a_period);
	}

    unpost_osrc_1 "widget/osrc"
	{
	up_a_batch_number "widget/parameter"	{ param_name=a_batch_number; type=integer; }
	up_a_ledger_number "widget/parameter"	{ param_name=a_ledger_number; type=string; }
	up_a_period "widget/parameter"		{ param_name=a_period; type=string; }

	replicasize=1; readahead=1; autoquery=never;
	sql = "	UPDATE
			/apps/kardia/data/Kardia_DB/a_subtrx_cashdisb/rows d
		SET
			:d:a_posted = 0,
			:d:a_paid_date = null,
			:d:a_paid_by = null
		WHERE
			:d:a_batch_number = :parameters:a_batch_number and
			:d:a_ledger_number = :parameters:a_ledger_number and
			:d:a_period = :parameters:a_period
		";

	end_unpost "widget/connector"
	    { 
	    event=EndQuery;
	    target=post_cd_batch;
	    action=Unposted;
	    a_batch_number = runclient(:unpost_osrc_1:a_batch_number);
	    a_ledger_number = runclient(:unpost_osrc_1:a_ledger_number);
	    a_period = runclient(:unpost_osrc_1:a_period); 
	    }
	}

    // Unpost a CD batch - process
    Post "widget/component-decl-action" { }
    Posted "widget/component-decl-event" { }
    post_cn "widget/connector"
	{
	event=Post;
	target=post_osrc_1;
	action=QueryParam;
	a_batch_number = runclient(:a_batch_number);
	a_ledger_number = runclient(:a_ledger_number);
	a_period = runclient(:a_period);
	}

    post_osrc_1 "widget/osrc"
	{
	p_a_batch_number "widget/parameter"	{ param_name=a_batch_number; type=integer; }
	p_a_ledger_number "widget/parameter"	{ param_name=a_ledger_number; type=string; }
	p_a_period "widget/parameter"		{ param_name=a_period; type=string; }

	replicasize=1; readahead=1; autoquery=never;
	sql = "	UPDATE
			/apps/kardia/data/Kardia_DB/a_subtrx_cashdisb/rows d
		SET
			:d:a_posted = 1,
			:d:a_paid_date = :d:a_effective_date,
			:d:a_paid_by = user_name()
		WHERE
			:d:a_batch_number = :parameters:a_batch_number and
			:d:a_ledger_number = :parameters:a_ledger_number and
			:d:a_period = :parameters:a_period
		;

		-- Mark payees via p_payee as 'payments allowed'.
		insert
		    /apps/kardia/data/Kardia_DB/p_payee/rows
		select
		    p_partner_key = :d:a_payee_partner_key,
		    a_gl_ledger_number = :d:a_ledger_number,
		    p_allow_payments = 1,
		    s_date_created=getdate(),
		    s_created_by=user_name(),
		    s_date_modified=getdate(),
		    s_modified_by=user_name()
		from
		    /apps/kardia/data/Kardia_DB/a_subtrx_cashdisb/rows d
		where
		    :d:a_ledger_number = :parameters:a_ledger_number and
		    :d:a_batch_number = :parameters:a_batch_number and
		    :d:a_period = :parameters:a_period
		group by
		    :d:a_payee_partner_key
		on duplicate
		    :a_gl_ledger_number,
		    :p_partner_key
		update set
		    :p_allow_payments = 1,
		    :s_date_modified=getdate() IF MODIFIED,
		    :s_modified_by=user_name() IF MODIFIED
		";

	end_post "widget/connector"
	    { 
	    event=EndQuery;
	    target=post_cd_batch;
	    action=Posted;
	    a_batch_number = runclient(:post_osrc_1:a_batch_number);
	    a_ledger_number = runclient(:post_osrc_1:a_ledger_number);
	    a_period = runclient(:post_osrc_1:a_period); 
	    }
	}

    // Unpost a CD batch from GL - process
    GLUnpost "widget/component-decl-action" { }
    GLUnposted "widget/component-decl-event" { }
    glunpost_cn "widget/connector"
	{
	event=GLUnpost;
	target=glunpost_osrc_1;
	action=QueryParam;
	a_batch_number = runclient(:a_batch_number);
	a_ledger_number = runclient(:a_ledger_number);
	a_period = runclient(:a_period);
	}

    glunpost_osrc_1 "widget/osrc"
	{
	gup_a_batch_number "widget/parameter"	{ param_name=a_batch_number; type=integer; }
	gup_a_ledger_number "widget/parameter"	{ param_name=a_ledger_number; type=string; }
	gup_a_period "widget/parameter"		{ param_name=a_period; type=string; }

	replicasize=1; readahead=1; autoquery=never;
	sql = "	DELETE
			/apps/kardia/data/Kardia_DB/a_transaction/rows t
		WHERE
			:t:a_batch_number = :parameters:a_batch_number and
			:t:a_origin = 'CD' and
			:a_ledger_number = :parameters:a_ledger_number and
			:t:a_period = :parameters:a_period
		;

		UPDATE
			/apps/kardia/data/Kardia_DB/a_subtrx_cashdisb/rows c
		SET
			:c:a_posted_to_gl = 0
		WHERE
			:c:a_batch_number = :parameters:a_batch_number and
			:c:a_ledger_number = :parameters:a_ledger_number and
			:c:a_period = :parameters:a_period
		";

	end_glunpost "widget/connector"
	    { 
	    event=EndQuery;
	    target=post_cd_batch;
	    action=GLUnposted;
	    a_batch_number = runclient(:glunpost_osrc_1:a_batch_number);
	    a_ledger_number = runclient(:glunpost_osrc_1:a_ledger_number);
	    a_period = runclient(:glunpost_osrc_1:a_period); 
	    }
	}

    // Post a CD batch - process
    GLPost "widget/component-decl-action" { }
    GLPosted "widget/component-decl-event" { }
    glpost_cn "widget/connector"
	{ 
	event=GLPost;
	target=glpost_osrc_1;
	action=QueryParam;
	a_batch_number = runclient(:a_batch_number);
	a_ledger_number = runclient(:a_ledger_number);
	a_period = runclient(:a_period);
	}
    glpost_osrc_1 "widget/osrc"
	{
	glp_a_batch_number "widget/parameter" { param_name=a_batch_number; type=integer; }
	glp_a_ledger_number "widget/parameter" { param_name=a_ledger_number; type=string; }
	glp_a_period "widget/parameter" { param_name=a_period; type=string; }
	replicasize=1; readahead=1; autoquery=never;
	sql = "	INSERT INTO
			/apps/kardia/data/Kardia_DB/a_transaction/rows
		SELECT 
			:c:a_ledger_number,
			a_batch_number = :parameters:a_batch_number,
			a_journal_number=1,
			:c:a_period,
			:c:a_effective_date,
			a_transaction_type='T',
			:c:a_cost_center,
			a_account_category=isnull((select :a:a_default_category from /apps/kardia/data/Kardia_DB/a_account/rows a where :a:a_ledger_number = :c:a_ledger_number and :a:a_account_code = :c:a_account_code), condition(:c:a_account_code >= '5000', '50', condition(:c:a_account_code >= '4000', '40', condition(:c:a_account_code <= '1999', '10', '20')))),
			:c:a_account_code,
			:c:a_amount,
			a_posted = 1,
			a_modified = 0,
			a_corrected = 0,
			a_correcting = 0,
			a_reconciled = 0,
			a_postprocessed = 0,
			a_postprocess_type = 'XX',
			a_origin = 'CD',
			a_receipt_sent = 0,
			a_receipt_desired = 0,
			a_first_gift = 0,
			a_sent_document_id = convert(string, :c:a_check_number),
			p_ext_partner_id = :c:a_payee_partner_key,
			a_goods_provided = $0.00,
			s_date_created = getdate(),
			s_date_modified = getdate(),
			s_created_by = user_name(),
			s_modified_by = user_name(),
			:c:a_comment
		FROM
			/apps/kardia/data/Kardia_DB/a_subtrx_cashdisb/rows c
		WHERE
			:c:a_batch_number = :parameters:a_batch_number and
			:c:a_ledger_number = :parameters:a_ledger_number and
			:c:a_period = :parameters:a_period and
			:c:a_voided = 0
		;

		INSERT INTO
			/apps/kardia/data/Kardia_DB/a_transaction/rows
		SELECT
			:c:a_ledger_number,
			a_batch_number = :parameters:a_batch_number,
			a_journal_number=1,
			:c:a_period,
			:c:a_effective_date,
			a_transaction_type='T',
			a_cost_center = isnull( (select :a_config_value from /apps/kardia/data/Kardia_DB/a_config/rows ac where :ac:a_ledger_number = :c:a_ledger_number and :ac:a_config_name = 'DisbCashFund'), 'GEN'),
			a_account_category=isnull((select :a:a_default_category from /apps/kardia/data/Kardia_DB/a_account/rows a where :a:a_ledger_number = :c:a_ledger_number and :a:a_account_code = :c:a_cash_account_code), '11'),
			a_account_code = :c:a_cash_account_code,
			a_amount = $0 - sum(:c:a_amount),
			a_posted = 1,
			a_modified = 0,
			a_corrected = 0,
			a_correcting = 0,
			a_reconciled = 0,
			a_postprocessed = 0,
			a_postprocess_type = 'XX',
			a_origin = 'CD',
			a_receipt_sent = 0,
			a_receipt_desired = 0,
			a_first_gift = 0,
			a_goods_provided = $0.00,
			s_date_created = getdate(),
			s_date_modified = getdate(),
			s_created_by = user_name(),
			s_modified_by = user_name(),
			a_comment = '' + count(:c:a_amount) + ' Disbursements'
		FROM
			/apps/kardia/data/Kardia_DB/a_subtrx_cashdisb/rows c
		WHERE
			:c:a_batch_number = :parameters:a_batch_number and
			:c:a_ledger_number = :parameters:a_ledger_number and
			:c:a_period = :parameters:a_period and
			:c:a_voided = 0
		GROUP BY
			:c:a_cash_account_code
		;
		
		INSERT INTO
			/apps/kardia/data/Kardia_DB/a_transaction/rows
		SELECT
			a_amount = $0 - sum(:t:a_amount),
			a_comment = '' + count(1) + ' transactions for ' + :c:a_bal_cost_center,
			a_account_code = '1900',
			a_cost_center=:c:a_bal_cost_center,
			:t:a_journal_number,
			:t:a_batch_number,
			:t:a_ledger_number,
			:t:a_period,
			:t:a_effective_date,
			:t:a_transaction_type,
			a_account_category = '19',
			a_posted = 1,
			a_modified = 0,
			a_corrected = 0,
			:t:a_correcting,
			:t:a_corrected_batch,
			:t:a_corrected_journal,
			:t:a_corrected_transaction,
			:t:a_reconciled,
			:t:a_postprocessed,
			:t:a_postprocess_type,
			:t:a_origin,
			a_receipt_sent = 0,
			a_receipt_desired = 0,
			a_first_gift = 0,
			a_goods_provided = $0,
			s_date_created = getdate(),
			s_created_by = user_name(),
			s_date_modified = getdate(),
			s_modified_by = user_name()
		FROM
			/apps/kardia/data/Kardia_DB/a_transaction/rows t,
			/apps/kardia/data/Kardia_DB/a_cost_center/rows c
		WHERE
			:t:a_ledger_number = :c:a_ledger_number and
			:t:a_cost_center = :c:a_cost_center and
			:t:a_ledger_number = :parameters:a_ledger_number and
			:t:a_batch_number = :parameters:a_batch_number
		GROUP BY
			:t:a_ledger_number, :t:a_batch_number, :t:a_journal_number, :c:a_bal_cost_center
		ORDER BY
			:t:a_cost_center
		HAVING
			:a_amount != $0
		;

		UPDATE
			/apps/kardia/data/Kardia_DB/a_subtrx_cashdisb/rows c
		SET
			:c:a_posted_to_gl = 1
		WHERE
			:c:a_batch_number = :parameters:a_batch_number and
			:c:a_ledger_number = :parameters:a_ledger_number and
			:c:a_period = :parameters:a_period and
			:c:a_voided = 0
		";

	end_glpost "widget/connector"
	    { 
	    event=EndQuery;
	    target=post_cd_batch;
	    action=GLPosted;
	    a_batch_number = runclient(:glpost_osrc_1:a_batch_number);
	    a_ledger_number = runclient(:glpost_osrc_1:a_ledger_number);
	    a_period = runclient(:glpost_osrc_1:a_period); 
	    }
	}
    }
