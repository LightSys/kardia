$Version=2$
plugin_base_config_rec "widget/component-decl"
    {
    width=828;
    height=490;
    title = "Bank Rec. Accounts";
    description = "Configure which accounts can be reconciled within the Bank Reconciliation app";
    sequence = 1300;

    vbox "widget/vbox"
	{
	x=0; y=0;
	width=828; height=490;
	spacing=7;

	ledger_label "widget/label" { height=16; style=bold; text="Accounting Ledger Selection:"; }

	// ledger selection
	ledger_osrc "widget/osrc"
	    {
	    sql = "select :name, label=:a_ledger_number + ' - ' + :a_ledger_desc, :a_ledger_number, row_num = row_number() from /apps/kardia/data/Kardia_DB/a_ledger/rows";
	    replicasize=256;
	    readahead=256;
	    autoquery=onload;

	    ledger_dd "widget/dropdown"
		{
		height=24; width=300;
		mode=objectsource;
		fieldname=label;
		numdisplay=12;
		}
	    }
	
	// Define config form here so fields can be spread throughout page as needed
	config_osrc "widget/osrc"
	    {
	    config_ledger_number "widget/parameter" { type=string;  param_name=a_ledger_number; }
	    sql = "select * from object expression('/apps/kardia/modules/gl/accounting_config.qyp/' + :parameters:a_ledger_number)";
	    
	    config_link "widget/rule"
		{
		ruletype=osrc_relationship;
		target=ledger_osrc;
		key_1=a_ledger_number;
		target_key_1=a_ledger_number;
		}
	    // form for all a_config fields
	    config_form "widget/form"{ } // leave form empty so can mix fields throughout page.
	    }
	// Query pivot for all of the a_is_reconcilable account fields
	recon_osrc "widget/osrc"
	    {
	    recon_ledger_number "widget/parameter" { type=string;  param_name=a_ledger_number; }
	    sql = "select * from object expression('/apps/kardia/modules/bank_rec/bank_rec_reconcilable.qyp/' + :parameters:a_ledger_number)";
	    
	    recon_link "widget/rule"
		{
		ruletype=osrc_relationship;
		target=ledger_osrc;
		key_1=a_ledger_number;
		target_key_1=a_ledger_number;
		}
	    // form for all a_config fields
	    recon_form "widget/form"{ } // leave form empty so can mix fields throughout page.
	    }
	// Query pivot for all of the a_is_customizable fields
	custom_osrc "widget/osrc"
	    {
	    custom_ledger_number "widget/parameter" { type=string;  param_name=a_ledger_number; }
	    sql = "select * from object expression('/apps/kardia/modules/bank_rec/bank_rec_customizable.qyp/' + :parameters:a_ledger_number)";
	    
	    custom_link "widget/rule"
		{
		ruletype=osrc_relationship;
		target=ledger_osrc;
		key_1=a_ledger_number;
		target_key_1=a_ledger_number;
		}
	    // form for all customizable fields
	    custom_form "widget/form"{ } // leave form empty so can mix fields throughout page.
	    }
	
	// date cuttoff for queries
	config_label "widget/label" { height=16; style=bold; form = config_form; text="History Cut Off Date:"; }
	f_cutoff_date "widget/component" 
	    { 
	    path="/sys/cmp/smart_field.cmp"; 
	    x=24; height=24; 
	    form=config_form;
	    field="ReconCutOff"; 
	    text="Update reconciliation records after:"; 
	    width=350; label_width=200; 
	    ctl_type=datetime; 
	    date_only=1; 
	    default_time='00:00:00'; 
	    recon_cutoff_hints "widget/hints" { style=notnull; } 
	    }
	
	account_header_label "widget/label" { height=20; style=bold; text="Account Settings:"; }

	// Each ledger needs a hidden tab so can switch between. Each tab needs a separate form for each account
	ledger_tab "widget/tab"
	    {
	    x=0; y=0; height=300;
	    // have the selected tab change with the drop down
	    ledger_select "widget/connector" { event=DataFocusChanged; action=SetTab; target=ledger_tab; source=ledger_osrc; TabIndex=runclient(:ledger_osrc:row_num); }
	    tab_location=none;

	    // each ledger needs its own tab
	    ledger_repeat "widget/repeat"
		{
		sql="SELECT * FROM /apps/kardia/data/Kardia_DB/a_ledger/rows;";

		account_tabpage "widget/tabpage"
		    {
		    title = runserver(convert('string', :ledger_repeat:a_ledger_number));
		    type=string;
		    accounts_box "widget/vbox"
			{
			// headers for the form feilds
			headers_box "widget/hbox"
			    {
			    height=30;
			    account_header "widget/label" { height=20; width=70; style=bold; text="Account:"; }
			    recon_header "widget/label"   { height=20; width=50; style=bold; text="Recon:"; }
			    custom_header "widget/label"  { height=20; width=50; style=bold; text="Custom:"; }
			    }

			// each account needs own label and fields. 
			accounts_repeat "widget/repeat"
			    {
			    sql = runserver("
				INSERT INTO
				    /apps/kardia/data/Kardia_DB/a_bank_recon_accts/rows
				SELECT
				    :a_ledger_number, :a_account_code, 
				    s_date_created  = getdate(), s_created_by  = user_name(), 
				    s_date_modified = getdate(), s_modified_by = user_name()
				FROM
				    /apps/kardia/data/Kardia_DB/a_account/rows
				ON DUPLICATE -- use upsert to avoid failing early on older values
				:a_ledger_number, :a_account_code 
				UPDATE SET
				:a_account_code = :a_account_code
				;

				SELECT 
				    *
				FROM
				    IDENTITY /apps/kardia/data/Kardia_DB/a_bank_recon_accts/rows 
				WHERE 
				    :a_ledger_number = "+:ledger_repeat:a_ledger_number+" 
				;");

			    // should work with just prexisting forms...?
			    account_box "widget/hbox"
				    {
				    height=15;
				    account_label "widget/label" {height=15; width=70; text=runserver(:accounts_repeat:a_account_code);}
				    account_rec_check "widget/checkbox" {height=15; width=50; form=recon_form; fieldname=runserver('x'+to_hex(:accounts_repeat:a_account_code)); accts_rec_hints "widget/hints" { style=notnull; } }
				    account_cust_check "widget/checkbox" {height=15; width=50; form=custom_form; fieldname=runserver('x'+to_hex(:accounts_repeat:a_account_code)); accts_cust_hints "widget/hints" { style=notnull; } }
				    }
			    }
			}
		    }
		}
	    }
	// Buttons: 
	button_box "widget/hbox"
	    {
	    height=1;
	    align=center;
	    spacing=10;

	    save_btn "widget/textbutton"
		{
		y=0; height=24; width=130;
		text = "Save";
		enabled=runclient(:config_form:is_savable OR :recon_form:is_savable OR :custom_form:is_savable);
		}
	    config_save "widget/connector" { event=Click; source=save_btn; target=config_form; event_condition=runclient(:config_form:is_savable); action=Save; }
	    recon_save "widget/connector" { event=Click; source=save_btn; target=recon_form; event_condition=runclient(:recon_form:is_savable); action=Save; }
	    custom_save "widget/connector" { event=Click; source=save_btn; target=custom_form; event_condition=runclient(:custom_form:is_savable); action=Save; }
	    cancel_btn "widget/textbutton"
		{
		y=0; height=24; width=130;
		text = "Cancel";
		enabled=runclient(:config_form:is_discardable OR :recon_form:is_discardable OR :custom_form:is_discardable);
		}
	    config_cancel "widget/connector" { event=Click; source=cancel_btn; target=config_form; event_condition=runclient(:config_form:is_discardable); action=Discard; }
	    recon_cancel "widget/connector" { event=Click; source=cancel_btn; target=recon_form; event_condition=runclient(:recon_form:is_discardable); action=Discard; }
	    custom_cancel "widget/connector" { event=Click; source=cancel_btn; target=custom_form; event_condition=runclient(:custom_form:is_discardable); action=Discard; }
	    }
	

	}
    }

