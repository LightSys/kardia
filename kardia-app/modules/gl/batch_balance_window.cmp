$Version=2$
batch_balance_window "widget/component-decl"
    {
    width=800;
    height=600;

    page "widget/parameter" { type=object; find_container="widget/page"; }

    ledger "widget/parameter" { type=string; default=null; allowchars="0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ"; deploy_to_client=yes; }

    Open "widget/component-decl-action" { }
    Open_cn "widget/connector" { event="Open"; target="tbwin"; action="Open"; Cascade=1; }

    tbwin "widget/childwindow"
	{
	title = runserver("Batch Balance Report - " + :this:ledger);
	width=580; height=550;
	x=10; y=25;
	visible = false;
	icon = "/apps/kardia/images/icons/gift.gif";

	rpt_form "widget/form"
	    {
	    allow_nodata = no;
	    allow_new = no;
	    auto_focus = no;

	    vb2 "widget/vbox"
		{
		x=32;y=8;width=514;height=508;
		spacing=4;
		//lbl_oth "widget/label" { height=30; font_size=16; text="Other Report Options"; align=center; }
		lbl_opt "widget/label" { height=30; font_size=16; text="Batch Balance - Report Options:"; align=center; }
		pn_sep1 "widget/pane" { height=2; style=lowered; }
		f_ledger "widget/component" { width=350; height=24; path="/sys/cmp/smart_field.cmp"; field='ledger'; ctl_type=label; text='Ledger:'; value=runserver(:this:ledger); form=rpt_form; }
		f_batch "widget/component" { width=350; height=24; path="/apps/kardia/modules/base/editbox_tree.cmp"; field="batch"; popup_source=runserver("/apps/kardia/modules/gl/batches.qyt/" + :this:ledger + "/"); popup_text="Choose Batch:"; text="Batch:"; attach_point=editbox; empty_desc = "required"; }
		f_costctr "widget/component" { width=350; height=24; path="/apps/kardia/modules/base/editbox_tree.cmp"; field="costctr"; popup_source=runserver("/apps/kardia/modules/gl/costctrs.qyt/" + :this:ledger + "/"); popup_text="Choose Cost Center:"; text="Cost Center:"; attach_point=editbox; empty_desc = "optional"; }
		//f_batch "widget/component" { width=350; height=24; path="/sys/cmp/smart_field.cmp"; field='batch'; ctl_type=dropdown; text='Select Batch:'; sql=runserver("select '' + :a_batch_number + ' - ' + :a_batch_desc, :a_batch_number from /apps/kardia/data/Kardia_DB/a_batch/rows where :a_ledger_number = " + quote(:this:ledger)); form=rpt_form; }

		sep "widget/autolayoutspacer" { height=4; }

		f_pagesep "widget/component" { x=10; width=400; height=24; path="/sys/cmp/smart_field.cmp"; field="pagesep"; ctl_type='checkboxleft'; text="Show cost centers on separate page(s)"; form=rpt_form; }
		f_summchart "widget/component" { x=10; width=400; height=24; path="/sys/cmp/smart_field.cmp"; field="summchart"; ctl_type='checkboxleft'; text="Include a ledger-wide GL Account summary"; form=rpt_form; }
		f_rollsubs "widget/component" { x=10; width=400; height=24; path="/sys/cmp/smart_field.cmp"; field="rollsubs"; ctl_type='checkboxleft'; text="Roll-up subsidiary cost centers (funds) with their main funds"; form=rpt_form; }

		sep1 "widget/autolayoutspacer" { height=4; }

		f_level "widget/component" { width=350; height=24; path="/sys/cmp/smart_field.cmp"; field='report_level'; ctl_type=dropdown; text='Detail Level:'; sql=runserver("select '' + :a_reporting_level + ' - ' + :a_level_rpt_desc, :a_reporting_level from /apps/kardia/data/Kardia_DB/a_reporting_level/rows where :a_ledger_number = " + quote(:this:ledger)); form=rpt_form; }
		f_docfmt "widget/component"
		    { 
		    width=350; height=24; 
		    path="/sys/cmp/smart_field.cmp"; 
		    field='document_format'; 
		    ctl_type=dropdown; 
		    text='Format:'; 
		    sql = runserver("select :t:type_description + ' (' + :t:type_name + ')', :t:type_name from /sys/cx.sysinfo/osml/types t, /sys/cx.sysinfo/prtmgmt/output_types ot where :t:type_name = :ot:type order by :t:type_description");
		    form=rpt_form;
		    }

		sep2 "widget/autolayoutspacer" { height=192; }
		pn_sep2 "widget/pane" { height=2; style=lowered; }

		ctls_hbox "widget/hbox"
		    {
		    height=32;
		    spacing=4;
		    rpt_print "widget/textbutton"
			{
			width=90;
			text="Print";
			enabled = runclient(char_length(:f_batch:content) > 0);
			rpt_print_cn "widget/connector" { event="Click"; target="rpt_form"; action="Submit"; Target=runclient("page"); NewPage=runclient(1); Source=runclient("/apps/kardia/modules/gl/batch_balance.rpt"); Width=runclient(800); Height=runclient(600); }
			}
		    rpt_cancel "widget/textbutton"
			{
			width=90;
			text="Cancel";
			rpt_cancel_cn "widget/connector" { event="Click"; target="tbwin"; action="Close"; }
			}
		    }
		}
	    }
	}
    }
