$Version=2$
init_payroll "widget/component-decl"
    {
    // Initialize payroll from a previous pay period
    InitPayroll "widget/component-decl-action" { }
    Initialized "widget/component-decl-event" { }
    init_cn "widget/connector"
	{
	event=InitPayroll;
	event_confirm = runclient("Really init payroll for ledger " + :a_ledger_number + ", group " + :a_payroll_group_id + ", date " + :paydate + ", period " + :payperiod + ", reference period " + :payrefperiod + ", based on period " + :paylastperiod + "?");
	target=init_osrc;
	action=QueryParam;
	event_all_params = 1;
	}

    init_osrc "widget/osrc"
	{
	a_ledger_number "widget/parameter"	{ type=string; }
	a_payroll_group_id "widget/parameter"	{ type=integer; }
	paydate "widget/parameter"		{ type=string; }
	payperiod "widget/parameter"		{ type=string; }
	payrefperiod "widget/parameter"		{ type=string; }
	paylastperiod "widget/parameter"	{ type=string; }

	replicasize=1; readahead=1; autoquery=never;

	done "widget/connector" { event=EndQuery; target=init_payroll; action=Initialized; }

	sql = " INSERT
			/apps/kardia/data/Kardia_DB/a_payroll_item/rows
		SELECT
			:a_ledger_number,
			:a_payroll_group_id,
			:a:a_payroll_id,
			:a:a_payroll_item_type_code,
			a_is_instance = 1,
			a_period = :parameters:payperiod,
			a_effective_date = convert(datetime, :parameters:paydate),
			:a:a_target_amount,
			:a:a_actual_amount,
			:a:a_filing_status,
			:a:a_allowances,
			:a:a_ref_cost_center,
			:a:a_ref_account_code,
			:a:a_xfer_cost_center,
			:a:a_xfer_account_code,
			s_date_created = getdate(),
			s_created_by = user_name(),
			s_date_modified = getdate(),
			s_modified_by = user_name()
		FROM
			/apps/kardia/data/Kardia_DB/a_payroll_item/rows a
		WHERE
			:a:a_period = :parameters:paylastperiod and
			:a:a_is_instance = 1 and
			:a:a_payroll_group_id = :parameters:a_payroll_group_id and
			:a:a_ledger_number = :parameters:a_ledger_number
		;
		UPDATE
			/apps/kardia/data/Kardia_DB/a_payroll/rows y,
			IDENTITY /apps/kardia/data/Kardia_DB/a_payroll_item/rows i
		SET
			:i:a_actual_amount = (select isnull($0 - sum(:t:a_amount),$0) FROM /apps/kardia/data/Kardia_DB/a_transaction/rows t WHERE :t:a_ledger_number = :y:a_ledger_number and :t:a_period <= :i:a_period and :t:a_period >= :parameters:payrefperiod and :t:a_cost_center = :i:a_ref_cost_center and :t:a_account_code >= '3000' and :t:a_account_code <= '5999')
		WHERE
			:y:a_payroll_id = :i:a_payroll_id and
			:y:a_ledger_number = :i:a_ledger_number and
			:y:a_payroll_group_id = :i:a_payroll_group_id and
			:i:a_is_instance = 1 and
			:y:a_ledger_number = :parameters:a_ledger_number and
			:i:a_period = :parameters:payperiod and
			:i:a_payroll_item_type_code = 'AVL'
		;
		UPDATE
			/apps/kardia/data/Kardia_DB/a_payroll/rows y,
			IDENTITY /apps/kardia/data/Kardia_DB/a_payroll_item/rows i
		SET
			:i:a_actual_amount = (select isnull($0 - sum(:t:a_amount),$0) FROM /apps/kardia/data/Kardia_DB/a_transaction/rows t WHERE :t:a_ledger_number = :y:a_ledger_number and :t:a_period = :i:a_period and :t:a_cost_center = :i:a_ref_cost_center and :t:a_account_code >= '4000' and :t:a_account_code <= '4999')
		WHERE
			:y:a_payroll_id = :i:a_payroll_id and
			:y:a_ledger_number = :i:a_ledger_number and
			:y:a_payroll_group_id = :i:a_payroll_group_id and
			:i:a_is_instance = 1 and
			:y:a_ledger_number = :parameters:a_ledger_number and
			:i:a_period = :parameters:payperiod and
			:i:a_payroll_item_type_code = 'DON'
		;
		UPDATE
			/apps/kardia/data/Kardia_DB/a_payroll/rows y,
			IDENTITY /apps/kardia/data/Kardia_DB/a_payroll_item/rows i
		SET
			:i:a_actual_amount = (select isnull(sum(:t:a_amount),$0) FROM /apps/kardia/data/Kardia_DB/a_transaction/rows t WHERE :t:a_ledger_number = :y:a_ledger_number and :t:a_period = :i:a_period and :t:a_cost_center = :i:a_ref_cost_center and :t:a_account_code >= '5000' and :t:a_account_code <= '5999')
		WHERE
			:y:a_payroll_id = :i:a_payroll_id and
			:y:a_ledger_number = :i:a_ledger_number and
			:y:a_payroll_group_id = :i:a_payroll_group_id and
			:i:a_is_instance = 1 and
			:y:a_ledger_number = :parameters:a_ledger_number and
			:i:a_period = :parameters:payperiod and
			:i:a_payroll_item_type_code = 'WKE'
		;
		UPDATE 
			/apps/kardia/data/Kardia_DB/a_payroll_item/rows i
		SET
			:i:a_actual_amount = $0
		WHERE
			:i:a_period = :parameters:payperiod and
			:i:a_payroll_group_id = :parameters:a_payroll_group_id and
			:i:a_ledger_number = :parameters:a_ledger_number and
			:i:a_payroll_item_type_code = 'ADV'
		;
		UPDATE
			/apps/kardia/data/Kardia_DB/a_payroll_item/rows i
		SET
			:i:a_actual_amount = $0
		WHERE
			:i:a_period = :parameters:payperiod and
			:i:a_payroll_group_id = :parameters:a_payroll_group_id and
			:i:a_ledger_number = :parameters:a_ledger_number and
			:i:a_payroll_item_type_code = 'GFT'
		";
	}
    }
