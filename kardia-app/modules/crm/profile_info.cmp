$Version=2$
profile_info "widget/component-decl"
    {
    width=380;
    height=110;

    Refresh "widget/component-decl-action" { }
    onRefresh "widget/connector" { event=Refresh; target=partner_info_osrc; action=Refresh; }

    partner_osrc "widget/parameter" { type=object; find_container="widget/osrc"; }

    partner_info_osrc "widget/osrc"
	{
	partner_key "widget/parameter" { type=string; }

	sql = "	select
		    txt = condition(not (char_length(:p:p_org_name) > 1), isnull(:p:p_given_name + ' ', '') + isnull(:p:p_surname,'') + condition(char_length(:p:p_preferred_name) > 1 and isnull(:p:p_preferred_name,'') != isnull(:p:p_given_name,''), ' (\"' + :p:p_preferred_name + '\")', ''), :p:p_org_name),
		    size = 'large'
		from
		    /apps/kardia/data/Kardia_DB/p_partner/rows p
		where
		    :p:p_partner_key = :parameters:partner_key
		;

		select
		    txt = 'Attn: ' + isnull(:p:p_given_name + ' ', '') + isnull(:p:p_surname,''),
		    size = 'medium'
		from
		    /apps/kardia/data/Kardia_DB/p_partner/rows p
		where
		    :p:p_partner_key = :parameters:partner_key and
		    char_length(:p:p_org_name) > 1 and
		    (char_length(:p:p_given_name) > 1 or
		     char_length(:p:p_surname) > 1)
		;

		select
		    txt = condition(char_length(isnull(:l:p_city,'')) > 1 or char_length(isnull(:l:p_state_province,'')) > 1, isnull(:l:p_city + ' ','') + isnull(:l:p_state_province + ' ',''), '') + '(ID# ' + :parameters:partner_key + ')',
		    size = 'medium'
		from
		    /apps/kardia/data/Kardia_DB/p_location/rows l
		where
		    :l:p_partner_key = :parameters:partner_key and
		    :l:p_revision_id = 0
		order by
		    :l:p_record_status_code asc,
		    :l:s_date_modified desc
		limit
		    1
		;

		select
		    txt = :ct:text + ': ' + isnull('+' + :c:p_phone_country + ' ', '') + isnull(:c:p_phone_area_city + ' ', '') + isnull(:c:p_contact_data,''),
		    size = 'small'
		from
		    /apps/kardia/data/Kardia_DB/p_contact_info/rows c,
		    /apps/kardia/data/Kardia_DB/_p_contact_type/rows ct
		where
		    :c:p_partner_key = :parameters:partner_key and
		    :ct:tag = :c:p_contact_type and
		    :c:p_record_status_code = 'A'
		";

	readahead=16;
	replicasize=16;

	link "widget/rule"
	    {
	    ruletype = osrc_relationship;
	    target = partner_osrc;
	    key_1 = partner_key;
	    target_key_1 = p_partner_key;
	    }

	table "widget/table"
	    {
	    x=0; y=0; width=300; height=110;
	    fl_width=0;
	    min_rowheight=10;
	    max_rowheight=64;
	    mode=dynamicrow;
	    allow_selection = yes;
	    show_selection = yes;
	    initial_selection = no;
	    demand_scrollbar = yes;
	    overlap_scrollbar = yes;
	    colsep = 0;
	    titlebar = no;
	    //row_border_color="#e0e0e0";
	    row_border_radius=0;
	    //row_shadow_offset=1;
	    //row_shadow_color="#c0c0c0";
	    inner_padding = 0;
	    rowhighlight_bgcolor = "#fff090";
	    textcolorhighlight = "#000000";
	    cellvspacing = 0;
	    row1_bgcolor = white;
	    row2_bgcolor = white;

	    col "widget/table-column" { fieldname=txt; style=runclient(condition(:partner_info_osrc:size != 'small', 'bold', '')); font_size=runclient(condition(:partner_info_osrc:size == 'large', 18, 11)); width=380; }
	    }
	}

    profile_info_vbox "widget/vbox"
	{
	condition=0;
	x=0; y=0;
	width=380;
	height=110;
	spacing=0;

	profile_hdr_name "widget/label"
	    {
	    height=26;
	    font_size=20;
	    style=bold;
	    value = runclient(condition(char_length(:partner_osrc:p_given_name) > 1 or char_length(:partner_osrc:p_surname) > 1, isnull(:partner_osrc:p_given_name + ' ', '') + isnull(:partner_osrc:p_surname,'') + condition(char_length(:partner_osrc:p_salutation) > 1 and isnull(:partner_osrc:p_salutation,'') != isnull(:partner_osrc:p_given_name,''), ' (\"' + :partner_osrc:p_salutation + '\")', ''), :partner_osrc:p_org_name));
	    }

	profile_hdr_contact "widget/label"
	    {
	    height=72;
	    font_size=11;
	    value = runclient(:partner_osrc:p_city + ' ' + :partner_osrc:p_state_province + '\n\n' + :partner_osrc:contact_data);
	    }

	edit_profile_hbox "widget/hbox"
	    {
	    condition=0;
	    height=13;
	    spacing=3;
	    fl_height=0;
	    
	    edit_profile_btn "widget/imagebutton"
		{
		width=11; height=11;
		image = "/apps/kardia/images/icons/pencil.png";

		epb_click "widget/connector" { event=Click; target=profile_info; action=Launch; Source="/apps/kardia/modules/base/partner_window.app"; Width=844; Height=600; id=runclient(:partner_osrc:p_partner_key); }
		}

	    edit_profile "widget/label"
		{
		y=1;
		font_size=11;
		width=365;
		widget_class=link;
		text = "Click here to update name and contact info";
		ep_click "widget/connector" { event=Click; target=profile_info; action=Launch; Source="/apps/kardia/modules/base/partner_window.app"; Width=844; Height=600; id=runclient(:partner_osrc:p_partner_key); }
		}
	    }
	}
    }
