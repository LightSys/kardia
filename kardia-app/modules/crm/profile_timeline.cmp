$Version=2$
profile_timeline "widget/component-decl"
    {
    width=442;
    height=450;

    // Refresh of our data
    Refresh "widget/component-decl-action" { }

    // Our events
    EditTask "widget/component-decl-event" { }
    EditContactHistory "widget/component-decl-event" { }
    TrackSelected "widget/component-decl-event" { }
    FileNeedsEditing "widget/component-decl-event" { }
    DataNeedsEditing "widget/component-decl-event" { }

    // Iconbar passed in so we can access the 'EnabledTypes' property it has
    iconbar "widget/parameter" { type=object; deploy_to_client=yes; }
    profile "widget/parameter" { type=object; deploy_to_client=yes; }

    // Collaborator osrc provides us information about the logged-in user
    collaborator_osrc "widget/parameter" { type=object; deploy_to_client=yes; }

    // Currently selected partner
    partner_osrc "widget/parameter" { type=object; find_container="widget/osrc"; }

    onRefresh "widget/connector" { event=Refresh; target=timeline_data_osrc; action=Refresh; }

    // This objectsource builds the activity list for the timeline.  We can then use that
    // list until the partner changes or a refresh is needed due to new data being added.
    //
    timeline_data_osrc "widget/osrc"
	{
	sql = "select :activity_group_id from '/apps/kardia/modules/crm/timeline.qy?partner_key=000000&my_cclist='";
	baseobj = "/apps/kardia/modules/crm/timeline.qy?partner_key=000000&my_cclist=";
	readahead=2;
	replicasize=2;
	autoquery=never;

	refresh_data_on_partner_change "widget/connector" { source=partner_osrc; event=EndQuery; action=ChangeSource; Source=runclient("/apps/kardia/modules/crm/timeline.qy?partner_key=" + :partner_osrc:p_partner_key + "&my_cclist=" + :collaborator_osrc:cclist); }

	refresh_timeline_on_data "widget/connector"
	    {
	    event=EndQuery;
	    target=timeline_osrc;
	    action=Refresh;
	    }

	refresh_chart_on_data "widget/connector"
	    {
	    event=EndQuery;
	    target=profile_timeline_graph;
	    action=LoadImage;
	    Source=runclient("/apps/kardia/modules/crm/timeline_svg.rpt?id=");
	    }
	}
    
    profile_timeline_vbox "widget/vbox"
	{
	x=0; y=0;
	width=442; height=450;
	spacing=8;

	timeline_scroll_pane "widget/pane"
	    {
	    height=410;
	    style=flat;

	    timeline_osrc "widget/osrc"
		{
		enabled_types "widget/parameter" { type=string; default=runclient(:iconbar:EnabledTypes); }
		my_id "widget/parameter" { type=string; default=runclient(:collaborator_osrc:p_partner_key); }

		sql = " DECLARE COLLECTION tmp_collection SCOPE APPLICATION;

			select
			    name = :e_activity_type + ':' + :e:e_reference_info,
			    img = :c:icon,
			    detail_height = :c:height,
			    :e:e_activity_date,
			    :e:e_activity_type,
			    :e:e_reference_info,
			    :e:e_info,
			    first_info = condition(charindex('\n', :e:e_info) > 0, substring(:e:e_info, 1, charindex('\n', :e:e_info) - 1), :e:e_info),
			    last_info = condition(charindex('\n', :e:e_info) > 0, substring(:e:e_info, charindex('\n', :e:e_info) + 1), null),
			    :e:p_partner_key,
			    shortdate = substring(convert(string,:e:e_activity_date),1,11),
			    datewhom = isnull(condition(:e:e_whom == :parameters:my_id, 'Me', condition(char_length(isnull(:p:p_org_name,'')) > 1, :p:p_org_name, condition(char_length(:p:p_preferred_name) > 1, :p:p_preferred_name, :p:p_given_name) + ' ' + :p:p_surname)) + ' ', '') + 'on ' + substring(convert(string,:e:e_activity_date),1,11)
			from
			    collection tmp_collection e,
			    --'/apps/kardia/modules/crm/timeline.qy?partner_key=000000&my_cclist=' e,
			    /apps/kardia/data/Kardia_DB/p_partner/rows p,
			    object wildcard '/apps/kardia/modules/crm/plugin_crm_timeline_*.cmp' c
			where
			    :p:p_partner_key =* :e:e_whom and
			    charindex(rtrim(:e:e_activity_type), :parameters:enabled_types) > 0 and
			    :c:type = rtrim(:e:e_activity_type)
			order by
			    :e:e_activity_date desc
			";
		readahead=50;
		replicasize=50;
		//baseobj = "/apps/kardia/modules/crm/timeline.qy?partner_key=000000&my_cclist=";

		refresh_on_options_change "widget/connector" { source=iconbar; event=TypesChange; action=Refresh; }

		timeline_table "widget/table"
		    {
		    x=0; y=0;
		    height=410;
		    width=442;
		    //row_border_color="#e0e0e0";
		    row_border_radius=4;
		    //row_shadow_offset=1;
		    //row_shadow_color="#c0c0c0";
		    demand_scrollbar = yes;
		    overlap_scrollbar = yes;
		    initial_selection = no;
		    colsep = 0;
		    titlebar = no;
		    min_rowheight = 16;
		    max_rowheight = 250;
		    cellvspacing = 4;
		    row1_bgcolor = white;
		    row2_bgcolor = white;
		    show_selection = yes;
		    inner_padding = 2;
		    rowhighlight_bgcolor = "#fff090";
		    textcolorhighlight = "#000000";
		    type_to_find = yes;

		    detail_rows "widget/repeat"
			{
			sql = "	select
				    path = :cx__pathname,
				    :type,
				    :height,
				    :icon
				from
				    object wildcard '/apps/kardia/modules/crm/plugin_crm_timeline_*.cmp'
				";

			detail_item "widget/table-row-detail"
			    {
			    condition=runserver(:detail_rows:height > 0);
			    display_for=runclient(:timeline_osrc:e_activity_type == runserver(:detail_rows:type));
			    height=runserver(:detail_rows:height + 8);
			    width=442;

			    detail_cmp "widget/component"
				{
				x=4; y=4;
				width=434;
				height=runserver(:detail_rows:height);
				fl_height=0;
				path = runserver(:detail_rows:path);
				partner_osrc = partner_osrc;
				timeline_osrc = timeline_osrc;
				timeline_data_osrc = timeline_data_osrc;
				profile = profile;
				collaborator_osrc = collaborator_osrc;

				on_track_click "widget/connector"
				    {
				    event=TrackSelected;
				    target=profile_timeline;
				    action=TrackSelected;
				    engagement_id=runclient(:engagement_id);
				    origin=runclient(:origin);
				    }

				on_reassign "widget/connector"
				    {
				    event=EditTask;
				    target=profile_timeline;
				    action=EditTask;
				    todo_id=runclient(:todo_id);
				    partner=runclient(:partner);
				    origin=runclient(:origin);
				    }

				on_editcont "widget/connector"
				    {
				    event=EditContactHistory;
				    target=profile_timeline;
				    action=EditContactHistory;
				    hist_id=runclient(:hist_id);
				    origin=runclient(:origin);
				    partner=runclient(:partner);
				    }

				on_editfile "widget/connector"
				    {
				    event=FileNeedsEditing;
				    target=profile_timeline;
				    action=FileNeedsEditing;
				    DocID=runclient(:DocID);
				    AssocID=runclient(:AssocID);
				    Filename=runclient(:Filename);
				    }

				on_editdata "widget/connector"
				    {
				    event=DataNeedsEditing;
				    target=profile_timeline;
				    action=DataNeedsEditing;
				    group=runclient(:group);
				    }
				}
			    }
			}

		    t_img "widget/table-column" { title=""; fieldname="img"; width=36; type=image; align=center; }
		    //t_date "widget/table-column" { title="Date"; fieldname="shortdate"; width=50; }
		    t_info "widget/table-column"
			{
			width=406;
			wrap=yes;
			title="Description";
			title_fieldname="first_info";
			title_style=bold;
			fieldname="last_info";
			caption_fieldname=datewhom;
			caption_textcolor = "#a0a0a0";
			caption_style=italic;
			}
		    }
		}
	    }

	profile_timeline_graph "widget/image"
	    {
	    height=32;
	    fl_width=10;
	    source="/sys/images/trans_1.gif";
	    //source=runclient("/apps/kardia/modules/crm/timeline_svg.rpt?partner=" + :partner_osrc:p_partner_key + "&cclist=" + :collaborator_osrc:cclist);
	    }
	}
    }
