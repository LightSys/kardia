$Version=2$
popover_newperson_simple "widget/component-decl"
    {
    width=1000; height=600;

    Open "widget/component-decl-action" { }

    PersonAdded "widget/component-decl-event" { }
    PartnerSelected "widget/component-decl-event" { }
    AdvancedAddPerson "widget/component-decl-event" { }

    onOpen1 "widget/connector" { event=Open; target=collab_var; action=SetValue; Value=runclient(:collaborator); }
    onOpen2 "widget/connector" { event=Open; target=point_var; action=SetValue; Value=runclient(:PointAt); }
    onOpen3 "widget/connector" { event=Open; target=newp_win; action=Open; IsModal=1; PointAt=runclient(:PointAt); PointSide=runclient(:PointSide); }

    collab_var "widget/variable" { type=string; }
    point_var "widget/variable" { type=object; }

    partner_var "widget/variable" { type=string; }

    dup_timer "widget/timer"
	{
	auto_start = 0;
	auto_reset = 1;
	msec = 500;

	onExpire "widget/connector"
	    {
	    event=Expire;
	    event_condition=runclient(char_length(:person_fname:content) > 2 or char_length(:person_lname:content) > 2 or char_length(:person_email:content) > 2);
	    target=dups_osrc;
	    action=QueryParam;
	    }
	}

    nextpartner "widget/component"
	{
	path = "/apps/kardia/modules/base/next_partner.cmp";

	got_key "widget/connector"
	    {
	    event=PartnerKey;
	    target=partner_var;
	    action=SetValue;
	    Value=runclient(:p_partner_key);
	    }
	got_key2 "widget/connector"
	    {
	    event=PartnerKey;
	    target=person_key;
	    action=SetValue;
	    Value=runclient(:p_partner_key);
	    }
	got_key3 "widget/connector" { event=PartnerKey; target=newemail_form; action=New; }
	got_key4 "widget/connector" { event=PartnerKey; target=newloc_form; action=New; }
	got_key5 "widget/connector" { event=PartnerKey; target=loc_person_key; action=SetValue; Value=runclient(:p_partner_key); }
	got_key6 "widget/connector" { event=PartnerKey; target=em_person_key; action=SetValue; Value=runclient(:p_partner_key); }
	}

    newp_win "widget/childwindow"
	{
	width=500; height=449;
	x=250; y=89;
	fl_y=0;
	titlebar=no;
	visible=no;
	border_radius=8;
	border_style=solid;
	border_color="#6080c0";
	shadow_radius=8;
	shadow_color="#6080c0";
	shadow_offset=0;
	background=null;
	bgcolor="#f8f8f8";

	new_on_open "widget/connector" { event=Open; target=newp_form; action=New; }
	clear_on_open "widget/connector" { event=Open; target=dups_osrc; action=Clear; }

	newp_vbox "widget/vbox"
	    {
	    x=15; y=15;
	    width=468; height=417;
	    spacing=8;

	    hdr_label "widget/label" { height=20; font_size=16; style=bold; value=runclient("New Person..."); align=center; }

	    explain_label "widget/label" { height=30; style=italic; value=runclient("Type a name & email address and click Add to quick-add a person, or click 'Advanced Add...' if you have a mailing address, phone number, etc."); }

	    newp_osrc "widget/osrc"
		{
		sql = "	select
			    *
			from
			    identity /apps/kardia/data/Kardia_DB/p_partner/rows p
			";
		baseobj = "/apps/kardia/data/Kardia_DB/p_partner/rows";
		replicasize=50;
		readahead=50;
		autoquery=never;

		newp_pane "widget/pane"
		    {
		    style=bordered;
		    border_radius=8;
		    border_color="#6080c0";
		    height=284;

		    newp_innervbox "widget/vbox"
			{
			x=10;
			y=10;
			width=446; height=274;
			spacing=8;

			newp_form "widget/form"
			    {
			    allow_search=no;
			    allow_view=no;
			    allow_modify=no;
			    allow_new=yes;
			    allow_obscure=yes;
			    next_form=newemail_form;
			    enter_mode=nextfield;

			    close_on_save "widget/connector" { event=DataSaved; target=newp_win; action=Close; }
			    refresh_profile_on_save "widget/connector" { event=DataSaved; target=popover_newperson_simple; action=PersonAdded; PartnerID=runclient(:partner_var:value); Origin=runclient(:point_var:value); event_delay=0.5; }
			    disc_email_on_discard "widget/connector" { event=Discard; target=newemail_form; action=Discard; }
			    disc_loc_on_discard "widget/connector" { event=Discard; target=newloc_form; action=Discard; }
			    close_on_discard "widget/connector" { event=Discard; target=newp_win; action=Close; }

			    getkey_on_new "widget/connector" { event=New; target=nextpartner; action=GetPartnerKey; }

			    person_fname "widget/component"
				{
				field=p_given_name;
				path="/sys/cmp/smart_field.cmp";
				height=20;
				text="First Name:";
				ctl_type=editbox;
				width=300;
				label_width=100;
				setTimer1 "widget/connector" { target=dup_timer; event=KeyPress; action=SetTimer; Time=300; AutoReset=0; }
				}
			    person_lname "widget/component"
				{
				field=p_surname;
				path="/sys/cmp/smart_field.cmp";
				height=20;
				text="Last Name:";
				ctl_type=editbox;
				width=300;
				label_width=100;
				setTimer2 "widget/connector" { target=dup_timer; event=KeyPress; action=SetTimer; Time=300; AutoReset=0; }
				}
			    person_comm "widget/component"
				{
				field=p_comments;
				path="/sys/cmp/smart_field.cmp";
				height=20;
				text="Comments:";
				ctl_type=editbox;
				width=415;
				label_width=100;
				}

			    person_key "widget/variable" { fieldname=p_partner_key; type=string; }
			    create_ofc "widget/variable" { fieldname=p_creating_office; type=string; office_hints "widget/hints" { default=runclient('OFC'); } }
			    person_class "widget/variable" { fieldname=p_partner_class; type=string; class_hints "widget/hints" { default=runclient('IND'); } }
			    stat1 "widget/variable" { fieldname=p_status_code; type=string; stat1_hints "widget/hints" { default=runclient('A'); } }
			    stat2 "widget/variable" { fieldname=p_record_status_code; type=string; stat2_hints "widget/hints" { default=runclient('A'); } }
			    sur_first "widget/variable" { fieldname=p_surname_first; type=string; sf_hints "widget/hints" { default=runclient(0); } }
			    no_solicit "widget/variable" { fieldname=p_no_solicitations; type=string; nosol_hints "widget/hints" { default=runclient(0); } }
			    no_mail "widget/variable" { fieldname=p_no_mail; type=string; nomail_hints "widget/hints" { default=runclient(0); } }

			    newperson_meta "widget/component" { visual=no; path="/apps/kardia/modules/base/record_metadata_hidden.cmp"; }
			    }

			newloc_osrc "widget/osrc"
			    {
			    sql = " select
					*
				    from
					identity /apps/kardia/data/Kardia_DB/p_location/rows l
				    ";
			    baseobj = "/apps/kardia/data/Kardia_DB/p_location/rows";
			    replicasize=50;
			    readahead=50;
			    autoquery=never;

			    partner_link_loc "widget/rule"
				{
				condition=0;
				ruletype=osrc_relationship;
				target=newp_osrc;
				key_1=p_partner_key;
				target_key_1=p_partner_key;
				}

			    newloc_form "widget/form"
				{
				auto_focus=no;
				allow_obscure=yes;
				loc_rec_stat_internal "widget/variable" { fieldname=p_record_status_code; type=string; loc_rec_stat_hints "widget/hints" { default=runclient('A'); } }
				loc_rev_internal "widget/variable" { fieldname=p_revision_id; type=integer; loc_rev_hints "widget/hints" { default=runclient(0); } }
				loc_type_internal "widget/variable" { fieldname=p_location_type; type=string; loc_type_hints "widget/hints" { default=runclient('H'); } }
				loc_person_key "widget/variable" { fieldname=p_partner_key; type=string; }

				newloc_meta "widget/component" { visual=no; path="/apps/kardia/modules/base/record_metadata_hidden.cmp"; }
				}
			    }

			newemail_osrc "widget/osrc"
			    {
			    sql = " select
					*
				    from
					identity /apps/kardia/data/Kardia_DB/p_contact_info/rows ci
				    ";
			    baseobj = "/apps/kardia/data/Kardia_DB/p_contact_info/rows";
			    replicasize=50;
			    readahead=50;
			    autoquery=never;

			    partner_link "widget/rule"
				{
				condition=0;
				ruletype=osrc_relationship;
				target=newp_osrc;
				key_1=p_partner_key;
				target_key_1=p_partner_key;
				}

			    newemail_form "widget/form"
				{
				auto_focus=no;
				allow_view=no;
				allow_obscure=yes;
				next_form=newp_form;
				enter_mode=nextfield;
				disc_partner_on_discard "widget/connector" { event=Discard; target=newp_form; action=Discard; }
				em_disc_loc_on_discard "widget/connector" { event=Discard; target=newloc_form; action=Discard; }
				em_close_on_discard "widget/connector" { event=Discard; target=newp_win; action=Close; }

				person_email "widget/component"
				    {
				    field=p_contact_data;
				    path="/sys/cmp/smart_field.cmp";
				    height=20;
				    text="Email:";
				    ctl_type=editbox;
				    width=300;
				    label_width=100;
				    setTimer3 "widget/connector" { target=dup_timer; event=KeyPress; action=SetTimer; Time=300; AutoReset=0; }
				    }
				em_person_key "widget/variable" { fieldname=p_partner_key; type=string; }

				em_type_internal "widget/variable" { fieldname=p_contact_type; type=string; em_type_hints "widget/hints" { default=runclient('E'); } }
				em_rec_stat_internal "widget/variable" { fieldname=p_record_status_code; type=string; em_rec_stat_hints "widget/hints" { default=runclient('A'); } }

				newemail_meta "widget/component" { path="/apps/kardia/modules/base/record_metadata_hidden.cmp"; }
				}
			    }

			newp_btns "widget/hbox"
			    {
			    height=24;
			    align=center;
			    spacing=10;

			    add_btn "widget/textbutton"
				{
				height=24; width=150;
				text = "Add";
				background="/apps/kardia/images/bg/lsblue_btngradient.png";
				fgcolor1=white;
				fgcolor2=black;
				disable_color="#334466";

				enabled = runclient(:newp_form:is_savable and :person_lname:value is not null and :person_fname:value is not null and :partner_var:value is not null);

				on_add1 "widget/connector" { event=Click; event_condition=runclient(not (char_length(:person_email:content) > 1)); target=newemail_form; action=Discard; event_delay=1.0; }
				on_add2 "widget/connector" { event=Click; target=newp_osrc; action=SaveClients; }
				on_add3 "widget/connector" { event=Click; target=newloc_osrc; action=SaveClients; }
				on_add4 "widget/connector" { event=Click; target=newemail_osrc; action=SaveClients; event_condition=runclient(char_length(:person_email:content) > 1); }
				}

			    cancel_btn "widget/textbutton"
				{
				height=24; width=150;
				text = runclient(condition(:newp_form:is_savable,"Cancel","Close"));
				background="/apps/kardia/images/bg/lsblue_btngradient.png";
				fgcolor1=white;
				fgcolor2=black;
				disable_color="#334466";

				on_cancel1 "widget/connector" { event=Click; target=newp_form; action=Discard; }
				on_cancel2 "widget/connector" { event=Click; target=newp_win; action=Close; }
				}
			    }

			dups_osrc "widget/osrc"
			    {
			    dup_fname "widget/parameter" { type=string; default=runclient(:person_fname:content); }
			    dup_lname "widget/parameter" { type=string; default=runclient(:person_lname:content); }
			    dup_email "widget/parameter" { type=string; default=runclient(:person_email:content); }

			    sql = " DELETE
					    /apps/kardia/data/Kardia_DB/p_dup_check_tmp/rows d
				    WHERE
					    :d:s_username = user_name()
				    ;

				    INSERT
					    /apps/kardia/data/Kardia_DB/p_dup_check_tmp/rows
				    SELECT
					    s_username = user_name(),
					    :ci:p_partner_key,
					    p_score = 90,
					    p_comment = 'Same Email',
					    s_date_created = getdate(),
					    s_created_by = user_name(),
					    s_date_modified = getdate(),
					    s_modified_by = user_name()
				    FROM
					    /apps/kardia/data/Kardia_DB/p_contact_info/rows ci
				    WHERE
					    :ci:p_contact_type = 'E' and
					    :ci:p_record_status_code = 'A' and
					    char_length(:ci:p_contact_data) >= 3 and
					    upper(:ci:p_contact_data) = upper(:parameters:dup_email)
				    ;

				    INSERT
					    /apps/kardia/data/Kardia_DB/p_dup_check_tmp/rows
				    SELECT
					    s_username = user_name(),
					    :p:p_partner_key,
					    p_score = 50,
					    p_comment = 'Similar Name',
					    s_date_created = getdate(),
					    s_created_by = user_name(),
					    s_date_modified = getdate(),
					    s_modified_by = user_name()
				    FROM
					    /apps/kardia/data/Kardia_DB/p_partner/rows p
				    WHERE
					    ((substring(upper(:p:p_surname),1,3) = substring(upper(:parameters:dup_lname),1,3) and
					      (substring(upper(:p:p_given_name),1,4) = substring(upper(:parameters:dup_fname),1,4) or substring(upper(:p:p_preferred_name),1,4) = substring(upper(:parameters:dup_fname),1,4))) or
					     (upper(:p:p_surname) = upper(:parameters:dup_lname) and
					      (substring(upper(:p:p_given_name),1,3) = substring(upper(:parameters:dup_fname),1,3) or substring(upper(:p:p_preferred_name),1,3) = substring(upper(:parameters:dup_fname),1,3))) or
					     (substring(upper(:p:p_surname),1,3) = substring(upper(:parameters:dup_lname),1,3) and
					      ((charindex(upper(:p:p_given_name), upper(:parameters:dup_fname)) > 0 or charindex(upper(:parameters:dup_fname), upper(:p:p_given_name)) > 0) or
					       (charindex(upper(:p:p_preferred_name), upper(:parameters:dup_fname)) > 0 or charindex(upper(:parameters:dup_fname), upper(:p:p_preferred_name)) > 0)))) and
					    ((char_length(rtrim(:p:p_surname)) - 1) * (char_length(:parameters:dup_lname) - 1) > 0 or (char_length(rtrim(:p:p_given_name)) - 1) * (char_length(:parameters:dup_fname) - 1) > 0)
				    ;

				    SELECT
					    img = isnull( ( select
							path = :doc:e_current_folder + '/' + :doc:e_current_filename
						    from
							/apps/kardia/data/Kardia_DB/e_partner_document/rows pd,
							/apps/kardia/data/Kardia_DB/e_document/rows doc,
							/apps/kardia/data/Kardia_DB/e_document_type/rows dt
						    where
							:pd:p_partner_key = :p:p_partner_key and
							:pd:e_document_id = :doc:e_document_id and
							:doc:e_doc_type_id = :dt:e_doc_type_id and
							:dt:e_doc_type_label = 'Profile Photo'
						    order by
							:pd:s_date_modified desc
						    limit
							1
						    ), '/apps/kardia/images/artwork/persona.png'),
					    :p:p_partner_key,
					    :d:p_score,
					    :d:p_comment,
					    :l:p_postal_code,
					    disp_name = condition(char_length(rtrim(:p:p_org_name)) > 1, :p:p_org_name + ' ' + condition(char_length(:p:p_given_name + :p:p_surname) > 1, '- ', ''), '') + isnull(:p:p_given_name + ' ','') + isnull(:p:p_surname + ' ',''),
					    disp_addr = condition(char_length(:l:p_address_1) > 1, :l:p_address_1 + ' ', '') + condition(char_length(:l:p_address_2) > 1, :l:p_address_2 + ' ', '') + condition(char_length(:l:p_address_3) > 1, :l:p_address_3 + ' ', ''),
					    disp_city = condition(char_length(:l:p_city) > 1, :l:p_city + ', ', '') + condition(char_length(:l:p_state_province) > 1, :l:p_state_province + ' ', ''),
					    stat_text = :sc:text,
					    score_asterisks = '*' * (:d:p_score / 20),
					    email_addr = (select sum(:p_contact_data + ', ') - ', ' from /apps/kardia/data/Kardia_DB/p_contact_info/rows ci where :ci:p_partner_key = :p:p_partner_key and :ci:p_record_status_code = 'A' and :ci:p_contact_type = 'E')
				    FROM
					    /apps/kardia/data/Kardia_DB/p_dup_check_tmp/rows d,
					    /apps/kardia/data/Kardia_DB/p_partner/rows p,
					    /apps/kardia/data/Kardia_DB/p_location/rows l,
					    /apps/kardia/data/Kardia_DB/_p_status_code/rows sc
				    WHERE
					    :d:s_username = user_name() and
					    -- (:parameters:exclude_partner_key is null or :d:p_partner_key != :parameters:exclude_partner_key) and
					    :p:p_partner_key = :d:p_partner_key and
					    :l:p_partner_key =* :p:p_partner_key and
					    :l:p_revision_id = 0 and
					    :sc:tag = :p:p_status_code
				    ORDER BY
					    :d:p_score desc
				    ";
			    autoquery=never;
			    replicasize=20;
			    readahead=20;

			    dups_table "widget/table"
				{
				height=110;
				width=446;
				min_rowheight=12;
				max_rowheight=64;
				allow_selection = yes;
				show_selection = yes;
				initial_selection = no;
				demand_scrollbar = yes;
				overlap_scrollbar = yes;
				colsep = 0;
				titlebar = no;
				row_border_radius=4;
				inner_padding = 2;
				cellvspacing = 2;
				row1_bgcolor = "#f8f8f8";
				row2_bgcolor = "#f8f8f8";
				rowhighlight_bgcolor = "#fff090";
				textcolorhighlight = "#000000";
				nodata_message = "Potential duplicates for your new Person will be displayed here.";

				on_click_arrow "widget/connector"
				    {
				    event=Click;
				    event_condition=runclient(:ColumnValue == '/sys/images/ico16ca.gif');
				    target=popover_newperson_simple;
				    action=PartnerSelected;
				    partner_key=runclient(:p_partner_key);
				    }
				on_click_arrow_2 "widget/connector"
				    {
				    event=Click;
				    event_condition=runclient(:ColumnValue == '/sys/images/ico16ca.gif');
				    target=newp_form;
				    action=Discard;
				    }
				on_click_arrow_3 "widget/connector"
				    {
				    event=Click;
				    event_condition=runclient(:ColumnValue == '/sys/images/ico16ca.gif');
				    target=newp_win;
				    action=Close;
				    }

				t_img "widget/table-column" { fieldname=img; width=16; type=image; image_maxwidth=12; image_maxheight=12; align=center; }
				t_dispname "widget/table-column" { value=runclient('' + :dups_osrc:p_partner_key + ' - ' + :dups_osrc:disp_name + isnull(', ' + :dups_osrc:disp_city, '')); width=366; caption_fieldname=email_addr; caption_textcolor='#a0a0a0'; wrap=yes; }
				//t_dispaddr "widget/table-column" { fieldname=disp_city; width=130; }
				t_goto "widget/table-column" { value=runclient('/sys/images/ico16ca.gif'); type=image; align=left; width=40; }
				}
			    }
			}
		    }

		sep1 "widget/autolayoutspacer" { height=1; }

		or_label "widget/label" { align=center; style=italic; text="Or, click Advanced Add below if you have more than just a name and email address."; }

		advnewp_btn "widget/hbox"
		    {
		    height=24;
		    align=center;
		    spacing=10;

		    advadd_btn "widget/textbutton"
			{
			height=24; width=200;
			text = "Advanced Add...";
			background="/apps/kardia/images/bg/lsblue_btngradient.png";
			fgcolor1=white;
			fgcolor2=black;
			disable_color="#334466";

			on_advadd1 "widget/connector"
			    {
			    event=Click;
			    target=popover_newperson_simple;
			    action=AdvancedAddPerson;
			    Origin=runclient(:point_var:value);
			    event_delay=0.5;
			    email=runclient(:person_email:value);
			    given_name=runclient(:person_fname:value);
			    surname=runclient(:person_lname:value);
			    comment=runclient(:person_comm:value);
			    }
			on_advadd2 "widget/connector" { event=Click; target=newp_form; action=Discard; }
			on_advadd3 "widget/connector" { event=Click; target=newp_win; action=Close; }
			}
		    }
		}
	    }
	}
    }
