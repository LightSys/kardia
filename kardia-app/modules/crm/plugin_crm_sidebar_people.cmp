$Version=2$
people_list "widget/component-decl"
    {
    height=417; width=248;

    title = "People";
    icon = "/apps/kardia/images/icons/ionicons-people.svg";
    sequence = 10;

    PartnerSelected "widget/component-decl-event" { }
    SetNumber "widget/component-decl-event" { }

    RefreshTasks "widget/component-decl-action" { }
    Refresh "widget/component-decl-action" { }
    onRefresh "widget/connector"
	{
	event=Refresh;
	target=active_collab_osrc;
	action=Refresh;
	}

    people_stats_osrc "widget/osrc"
	{
	collaborator "widget/parameter" { type=string; default=runclient(null); }
	sql = "	select
		    new_today_cnt = (select
					count(1)
				    from
					/apps/kardia/data/Kardia_DB/p_partner/rows p
				    where
					:p:s_created_by = user_name() and
					:p:s_date_created >= dateadd(day, -1, getdate())
				    ),
		    all_collab_cnt = (select
					count(1)
				    from
					/apps/kardia/data/Kardia_DB/e_collaborator/rows c
				    where
					:c:e_collaborator = :parameters:collaborator
				    ),
		    new_collab_cnt = (select
					count(1)
				    from
					/apps/kardia/data/Kardia_DB/e_collaborator/rows c
				    where
					:c:e_collaborator = :parameters:collaborator and
					(select count(1) from /apps/kardia/data/Kardia_DB/e_ack/rows a where :a:e_object_type = 'e_collaborator' and :a:e_object_id = :c:name and :a:e_whom = :parameters:collaborator) = 0
				    )
		";
	replicasize=2;
	readahead=2;
	indicates_activity=no;

	set_cnt_on_load "widget/connector"
	    {
	    event=EndQuery;
	    target=people_list;
	    action=SetNumber;
	    Which=runclient("New");
	    Number=runclient(condition(:people_stats_osrc:new_today_cnt > 0, '+' + :people_stats_osrc:new_today_cnt, ''));
	    Color=runclient(condition(:people_stats_osrc:new_today_cnt > 0, '#00c000', 'black'));
	    }
	set_unseencnt_on_load "widget/connector"
	    {
	    event=EndQuery;
	    target=people_list;
	    action=SetNumber;
	    Which=runclient("People");
	    Number=runclient(condition(:people_stats_osrc:new_collab_cnt > 0, '' + (:people_stats_osrc:all_collab_cnt - :people_stats_osrc:new_collab_cnt) + '+' + :people_stats_osrc:new_collab_cnt, :people_stats_osrc:all_collab_cnt));
	    Color=runclient(condition(:people_stats_osrc:new_collab_cnt > 0, 'red', 'black'));
	    }
	}

    people_vbox "widget/vbox"
	{
	x=0; y=0; width=248; height=413;
	spacing=8;

	active_collab_osrc "widget/osrc"
	    {
	    sql = " select
			collaborator_id = :c:e_collaborator,
			collaborator_name = (select condition(char_length(isnull(:p_org_name,'')) > 1, :p_org_name, :p_given_name + ' ' + :p_surname) from /apps/kardia/data/Kardia_DB/p_partner/rows where :p_partner_key = :c:e_collaborator),
			collaborator_type_id = :c:e_collab_type_id, 
			collaborator_type = (select :e_collab_type_label from /apps/kardia/data/Kardia_DB/e_collaborator_type/rows where :e_collab_type_id = :c:e_collab_type_id),
			partner_id = :c:p_partner_key,
			partner_name = (select condition(char_length(isnull(:p_org_name,'')) > 1, :p_org_name, :p_given_name + ' ' + :p_surname) from /apps/kardia/data/Kardia_DB/p_partner/rows where :p_partner_key = :c:p_partner_key),
			role_id = :c:e_collab_type_id,
			role_name = :ct:e_collab_type_desc,
			track_ids = (select sum('X' + :pe:e_track_id + 'X') from /apps/kardia/data/Kardia_DB/e_partner_engagement/rows pe where :pe:p_partner_key = :c:p_partner_key and :pe:e_is_archived = 0 group by :pe:e_track_id),
			track_names = (select sum(first(:t:e_track_name) + ', ') - ', ' from /apps/kardia/data/Kardia_DB/e_partner_engagement/rows pe, /apps/kardia/data/Kardia_DB/e_engagement_track/rows t where :pe:p_partner_key = :c:p_partner_key and :pe:e_is_archived = 0 and :t:e_track_id = :pe:e_track_id group by :pe:e_track_id),
			img = isnull( ( select
				    path = :d:e_current_folder + '/' + :d:e_current_filename
				from
				    /apps/kardia/data/Kardia_DB/e_document/rows d,
				    /apps/kardia/data/Kardia_DB/e_partner_document/rows pd,
				    /apps/kardia/data/Kardia_DB/e_document_type/rows dt
				where
				    :dt:e_doc_type_label = 'Profile Photo' and
				    :d:e_doc_type_id = :dt:e_doc_type_id and
				    :pd:e_document_id = :d:e_document_id and
				    :pd:p_partner_key = :c:p_partner_key
				order by
				    :pd:s_date_modified desc
				limit
				    1
				), '/apps/kardia/images/artwork/persona.png'),
			last_date = substring(convert(string, (select max(:e_contact_date) from /apps/kardia/data/Kardia_DB/e_contact_history/rows ch where :ch:p_partner_key = :c:p_partner_key) ),1,11),
			is_new = condition((select count(1) from /apps/kardia/data/Kardia_DB/e_ack/rows a where :a:e_object_type = 'e_collaborator' and :a:e_object_id = :c:name and :a:e_whom = :c:e_collaborator) = 0, 1, 0)
		    from
			identity /apps/kardia/data/Kardia_DB/e_collaborator/rows c,
			/apps/kardia/data/Kardia_DB/p_staff/rows s,
			/apps/kardia/data/Kardia_DB/e_collaborator_type/rows ct
		    where
			:c:e_collaborator = :s:p_partner_key and
			:s:p_kardia_login = user_name() and
			:ct:e_collab_type_id = :c:e_collab_type_id
		    ";
	    readahead=50;
	    replicasize=50;
	    use_having_clause=1;
	    refresh_interval=30000;
	    
	    stats_link "widget/connector"
		{
		event=EndQuery;
		target=people_stats_osrc;
		action=QueryParam;
		collaborator=runclient(:active_collab_osrc:collaborator_id);
		}

	    active_collab_table "widget/table"
		{
		height=381;
		//rowheight = 20;
		min_rowheight=16;
		max_rowheight=72;
		mode=dynamicrow;
		allow_selection = yes;
		show_selection = yes;
		initial_selection = no;
		demand_scrollbar = yes;
		overlap_scrollbar = yes;
		colsep = 0;
		titlebar = no;
		//row_border_color="#e0e0e0";
		row_border_radius=4;
		//row_shadow_offset=1;
		//row_shadow_color="#c0c0c0";
		inner_padding = 2;
		rowhighlight_bgcolor = "#fff090";
		textcolorhighlight = "#000000";
		cellvspacing = 4;
		row1_bgcolor = runclient(condition(:active_collab_osrc:is_new == 1, "#80ff80", "#f8f8f8"));
		row2_bgcolor = runclient(condition(:active_collab_osrc:is_new == 1, "#80ff80", "#f8f8f8"));
		nodata_message = "(no people to show)";

		onclick "widget/connector" { event=Click; target=people_list; action=PartnerSelected; partner_key=runclient(:partner_id); }

		t_img "widget/table-column" { fieldname="img"; width=40; type=image; image_maxwidth=32; image_maxheight=32; align=center; }
		t_name "widget/table-column"
		    {
		    value=runclient(:active_collab_osrc:partner_name + condition(:active_collab_osrc:is_new = 1, ' (new)', ''));
		    width=208;
		    caption_value=runclient(
			    isnull('Tracks: ' + :active_collab_osrc:track_names + '\n', '') +
			    isnull('My Role: ' + :active_collab_osrc:role_name + '\n', '') +
			    isnull('Last Communication: ' + :active_collab_osrc:last_date + '\n', '')
			    );
		    caption_textcolor="#9b8707";
		    style=bold;
		    font_size=15;
		    caption_style=italic;
		    wrap=yes;
		    }
		}
	    }

	role_select_dd "widget/dropdown"
	    {
	    x=4;
	    height=24;
	    width=240;
	    mode=dynamic_server;
	    numdisplay=16;
	    sql = " select
			'All of My People',
			'A',
			1
		    ;

		    select
			'New People Only',
			'N',
			0
		    ;

		    select
			'My Role: ' + :e_collab_type_label,
			'R' + convert(string,:e_collab_type_id),
			0
		    from
			/apps/kardia/data/Kardia_DB/e_collaborator_type/rows
		    ;

		    select
			'Track: ' + :e_track_name,
			'T' + convert(string,:e_track_id),
			0
		    from
			/apps/kardia/data/Kardia_DB/e_engagement_track/rows
		    ";

	    role_dd_hints "widget/hints" { style=notnull; }

	    on_role_select_all "widget/connector"
		{
		event=DataChange;
		event_condition=runclient(substring(:Value,1,1) == 'A');
		target=active_collab_osrc;
		action=QueryParam;
		}
	    on_role_select_new "widget/connector"
		{
		event=DataChange;
		event_condition=runclient(substring(:Value,1,1) == 'N');
		target=active_collab_osrc;
		action=QueryParam;
		is_new=1;
		}
	    on_role_select_role "widget/connector"
		{
		event=DataChange;
		event_condition=runclient(substring(:Value,1,1) == 'R');
		target=active_collab_osrc;
		action=QueryParam;
		role_id=runclient(convert(integer,substring(:Value,2)));
		}
	    on_role_select_track "widget/connector"
		{
		event=DataChange;
		event_condition=runclient(substring(:Value,1,1) == 'T');
		target=active_collab_osrc;
		action=QueryText;
		query=runclient('X' + substring(:Value,2) + 'X');
		field_list="*track_ids*";
		}
	    }
	}
    }
