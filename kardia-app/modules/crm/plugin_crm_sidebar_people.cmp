$Version=2$
people_list "widget/component-decl"
    {
    height=417; width=248;

    title = "People";
    icon = "/apps/kardia/images/icons/ionicons-people.svg";
    sequence = 10;

    PartnerSelected "widget/component-decl-event" { }
    SetNumber "widget/component-decl-event" { }
    CollabChanged "widget/component-decl-event" { }
    AddPerson "widget/component-decl-event" { }

    RefreshTasks "widget/component-decl-action" { }
    Refresh "widget/component-decl-action" { }
    onRefresh "widget/connector"
	{
	event=Refresh;
	target=active_collab_osrc;
	action=Refresh;
	}

    collaborator_osrc "widget/parameter" { type=object; }

    people_stats_osrc "widget/osrc"
	{
	collaborator "widget/parameter" { type=string; default=runclient(null); }
	sql = "	select
		    new_today_cnt = (select
					count(1)
				    from
					/apps/kardia/data/Kardia_DB/p_partner/rows p
				    where
					:p:s_created_by = user_name() and
					:p:s_date_created >= dateadd(day, -1, getdate())
				    ),
		    all_collab_cnt = (select
					count(1)
				    from
					/apps/kardia/data/Kardia_DB/e_collaborator/rows c
				    where
					:c:e_collaborator = :parameters:collaborator
				    ),
		    new_collab_cnt = (select
					count(1)
				    from
					/apps/kardia/data/Kardia_DB/e_collaborator/rows c
				    where
					:c:e_collaborator = :parameters:collaborator and
					isnull((select 1 from /apps/kardia/data/Kardia_DB/e_ack/rows a where :a:e_object_type = 'e_collaborator' and :a:e_object_id = :c:name and :a:e_whom = :parameters:collaborator and :a:e_ack_type = 4 limit 1),0) = 0
				    )
		";
	replicasize=2;
	readahead=2;
	indicates_activity=no;
	autoquery=never;

	set_cnt_on_load "widget/connector"
	    {
	    event=EndQuery;
	    target=people_list;
	    action=SetNumber;
	    Which=runclient("New");
	    Number=runclient(condition(:people_stats_osrc:new_today_cnt > 0, '+' + :people_stats_osrc:new_today_cnt, ''));
	    Color=runclient(condition(:people_stats_osrc:new_today_cnt > 0, '#00c000', 'black'));
	    }
	set_unseencnt_on_load "widget/connector"
	    {
	    event=EndQuery;
	    target=people_list;
	    action=SetNumber;
	    Which=runclient("People");
	    //Number=runclient(condition(:people_stats_osrc:new_collab_cnt > 0, '' + (:people_stats_osrc:all_collab_cnt - :people_stats_osrc:new_collab_cnt) + '+' + :people_stats_osrc:new_collab_cnt, :people_stats_osrc:all_collab_cnt));
	    Number=runclient(condition(:people_stats_osrc:new_collab_cnt > 0, '+' + :people_stats_osrc:new_collab_cnt, :people_stats_osrc:all_collab_cnt));
	    Color=runclient(condition(:people_stats_osrc:new_collab_cnt > 0, 'red', 'black'));
	    }
	}

    people_vbox "widget/vbox"
	{
	x=0; y=0; width=248; height=413;
	spacing=8;

	active_collab_osrc "widget/osrc"
	    {
	    act_collaborator "widget/parameter" { param_name=collaborator; type=string; default=runclient(:collaborator_osrc:p_partner_key); }
	    search_tag1 "widget/parameter" { type=integer; }
	    search_tag2 "widget/parameter" { type=integer; }
	    search_track "widget/parameter" { type=integer; }
	    search_newonly "widget/parameter" { type=integer; }
	    search_role "widget/parameter" { type=integer; }
	    search_stat "widget/parameter" { type=string; }
	    search_all "widget/parameter" { type=string; }

	    sql = " declare object info;

		    select
			:info:partner = condition(:parameters:search_all = 'M', :s:p_partner_key, condition(:parameters:search_all = 'C', '*', :parameters:search_all)),
			:info:collab = :s:p_partner_key
		    where
			:s:p_kardia_login = user_name()
		    from
			/apps/kardia/data/Kardia_DB/p_staff/rows s
		    ;
		    
		    select
			:c:p_partner_key,
			collaborator_id = :c:e_collaborator,
			collab_me = sum(condition(:c2:e_collaborator is not null, 1, 0)),
			collab_one = condition(:parameters:search_all != 'M' and :parameters:search_all != 'C' and :parameters:search_all != :info:collab, 1, 0),
			collaborator_name = (select condition(char_length(isnull(:p_org_name,'')) > 1, :p_org_name, :p_given_name + ' ' + :p_surname) from /apps/kardia/data/Kardia_DB/p_partner/rows where :p_partner_key = :c:e_collaborator),
			collaborator_firstname = (select condition(char_length(isnull(:p_org_name,'')) > 1, :p_org_name, :p_given_name) from /apps/kardia/data/Kardia_DB/p_partner/rows where :p_partner_key = :c:e_collaborator),
			collaborator_type_id = :c:e_collab_type_id, 
			collaborator_me_type_id = :c2:e_collab_type_id, 
			collaborator_type = (select :e_collab_type_label from /apps/kardia/data/Kardia_DB/e_collaborator_type/rows where :e_collab_type_id = :c:e_collab_type_id),
			collaborator_status = :c:e_collaborator_status,
			collaborator_me_status = :c2:e_collaborator_status,
			partner_id = :c:p_partner_key,
			partner_name = (select condition(char_length(isnull(:p_org_name,'')) > 1, :p_org_name, :p_given_name + ' ' + :p_surname) from /apps/kardia/data/Kardia_DB/p_partner/rows where :p_partner_key = :c:p_partner_key),
			role_id = :c:e_collab_type_id,
			role_name = :ct:e_collab_type_desc,
			my_role_name = :ct2:e_collab_type_desc,
			track_ids = (select sum('X' + :pe:e_track_id + 'X') from /apps/kardia/data/Kardia_DB/e_partner_engagement/rows pe where :pe:p_partner_key = :c:p_partner_key and :pe:e_is_archived = 0 group by :pe:e_track_id),
			track_names = (select sum(first(:t:e_track_name) + ', ') - ', ' from /apps/kardia/data/Kardia_DB/e_partner_engagement/rows pe, /apps/kardia/data/Kardia_DB/e_engagement_track/rows t where :pe:p_partner_key = :c:p_partner_key and :pe:e_is_archived = 0 and :t:e_track_id = :pe:e_track_id group by :pe:e_track_id),
			tag_ids = (select sum('X' + :t:e_tag_id + 'X') from /apps/kardia/data/Kardia_DB/e_tag/rows t where :t:p_partner_key = :c:p_partner_key and :t:e_tag_strength > 0.01),
			img = isnull( ( select
				    path = :d:e_current_folder + '/' + :d:e_current_filename
				from
				    /apps/kardia/data/Kardia_DB/e_partner_document/rows pd,
				    /apps/kardia/data/Kardia_DB/e_document/rows d,
				    /apps/kardia/data/Kardia_DB/e_document_type/rows dt
				where
				    :pd:p_partner_key = :c:p_partner_key and
				    :pd:e_document_id = :d:e_document_id and
				    :d:e_doc_type_id = :dt:e_doc_type_id and
				    :dt:e_doc_type_label = 'Profile Photo'
				order by
				    :pd:s_date_modified desc
				limit
				    1
				), '/apps/kardia/images/artwork/persona.png'),
			last_date = substring(convert(string, (select max(:e_contact_date) from /apps/kardia/data/Kardia_DB/e_contact_history/rows ch where :ch:p_partner_key = :c:p_partner_key) ),1,11),
			is_new = condition(datediff(hour,isnull((select :a:s_date_created from /apps/kardia/data/Kardia_DB/e_ack/rows a where :a:e_object_type = 'e_collaborator' and :a:e_object_id = :c:name and :a:e_whom = :c:e_collaborator and :a:e_ack_type = 4), getdate()),getdate()) <= 24, 1, 0)
		    from
			identity /apps/kardia/data/Kardia_DB/e_collaborator/rows c,
			/apps/kardia/data/Kardia_DB/e_collaborator/rows c2,
			/apps/kardia/data/Kardia_DB/e_collaborator_type/rows ct,
			/apps/kardia/data/Kardia_DB/e_collaborator_type/rows ct2
		    where
			(:c:e_collaborator = :info:partner or :info:partner = '*') and
			:ct:e_collab_type_id = :c:e_collab_type_id and
			charindex(:c:e_collaborator_status, isnull(:parameters:search_stat, 'AP')) > 0 and
			:c2:p_partner_key =* :c:p_partner_key and
			:c2:e_collaborator = :info:collab and
			:ct2:e_collab_type_id = :c2:e_collab_type_id
		    group by
			:c:p_partner_key
		    having
			(:parameters:search_tag1 is null or charindex('X' + :parameters:search_tag1 + 'X', :tag_ids) > 0) and
			(:parameters:search_tag2 is null or charindex('X' + :parameters:search_tag2 + 'X', :tag_ids) > 0) and
			(:parameters:search_track is null or charindex('X' + :parameters:search_track + 'X', :track_ids) > 0) and
			(:parameters:search_role is null or :parameters:search_role = :role_id) and
			(:parameters:search_newonly is null or :parameters:search_newonly = 0 or :parameters:search_newonly = :is_new)
		    ";
	    readahead=600;
	    replicasize=600;
	    //use_having_clause=1;
	    refresh_interval=30000;
	    autoquery=never; // triggered by customize_form entering New state.
	    
	    stats_link "widget/connector"
		{
		event=EndQuery;
		target=people_stats_osrc;
		action=QueryParam;
		//collaborator=runclient(:active_collab_osrc:collaborator_id);
		collaborator=runclient(:collaborator_osrc:p_partner_key);
		}

	    active_collab_table "widget/table"
		{
		height=372;
		//rowheight = 20;
		min_rowheight=16;
		max_rowheight=80;
		mode=dynamicrow;
		allow_selection = yes;
		allow_deselection = no;
		show_selection = yes;
		initial_selection = no;
		demand_scrollbar = yes;
		overlap_scrollbar = yes;
		colsep = 0;
		titlebar = no;
		//row_border_color="#e0e0e0";
		row_border_radius=4;
		//row_shadow_offset=1;
		//row_shadow_color="#c0c0c0";
		inner_padding = 2;
		rowhighlight_bgcolor = "#fff090";
		textcolorhighlight = "#000000";
		cellvspacing = 4;
		row1_bgcolor = runclient(condition(:active_collab_osrc:is_new == 1, "#80ff80", "#f8f8f8"));
		row2_bgcolor = runclient(condition(:active_collab_osrc:is_new == 1, "#80ff80", "#f8f8f8"));
		nodata_message = "(no people to show)";
		type_to_find = yes;

		onclick "widget/connector" { event=Click; target=people_list; action=PartnerSelected; partner_key=runclient(:partner_id); }

		t_img "widget/table-column" { fieldname="img"; width=40; type=image; image_maxwidth=32; image_maxheight=32; align=center; }
		t_name "widget/table-column"
		    {
		    value=runclient(:active_collab_osrc:partner_name + condition(:active_collab_osrc:is_new = 1, ' (new)', ''));
		    width=208;
		    caption_value=runclient(
			    isnull('Tracks: ' + :active_collab_osrc:track_names + '\n', '') +
			    isnull(condition(not :active_collab_osrc:collab_one, null, :active_collab_osrc:collaborator_firstname + "'s Role: " + :active_collab_osrc:role_name + '\n'), '') +
			    isnull("My Role: " + :active_collab_osrc:my_role_name + '\n', '') +
			    isnull('Last Communication: ' + :active_collab_osrc:last_date + '\n', '')
			    );
		    caption_textcolor=runclient(condition(:active_collab_osrc:collaborator_status = 'A', '#9b8707', condition(:active_collab_osrc:collaborator_status = 'I', '#cdc383', '#00a000')));
		    textcolor=runclient(condition(:active_collab_osrc:collaborator_status = 'A', 'black', condition(:active_collab_osrc:collaborator_status = 'I', '#a0a0a0', '#008000')));
		    style=bold;
		    font_size=15;
		    caption_style=italic;
		    wrap=yes;
		    }

		editcollab_me_detail "widget/table-row-detail"
		    {
		    height=34;
		    width=248;
		    display_for=runclient(:active_collab_osrc:collab_me);

		    editcollab_me_form "widget/form"
			{
			//allow_obscure=yes;
			confirm_discard=no;

			refresh_profile_me_on_save2 "widget/connector" { event=DataSaved; target=people_list; action=CollabChanged; }
			refresh_profile_me_on_delete "widget/connector" { event=DataDeleted; target=people_list; action=CollabChanged; }

			editcollab_me_hbox "widget/hbox"
			    {
			    x=4; y=10; height=24; width=240;
			    spacing=10;

			    editcollab_me_meta "widget/component" { path="/apps/kardia/modules/base/record_metadata_hidden.cmp"; visual=no; }

			    editcollab_me_type "widget/component"
				{
				field=collaborator_me_type_id;
				path="/sys/cmp/smart_field.cmp";
				height=24;
				text="My Role:";
				ctl_type=dropdown;
				sql="select :e_collab_type_label, :e_collab_type_id from /apps/kardia/data/Kardia_DB/e_collaborator_type/rows order by :e_collab_type_label";
				width=110;
				label_width=55;
				numdisplay=10;

				role_me_hints "widget/hints" { style=notnull; }

				save_on_data_change_me "widget/connector" { event=DataChange; event_delay=0.05; target=editcollab_me_form; action=Save; }

				discard_on_lose_focus_me "widget/connector" { event=LoseFocus; event_delay=0.4; target=editcollab_me_form; action=Discard; }
				}

			    editstat_me_type "widget/component"
				{
				field=collaborator_me_status;
				path="/sys/cmp/smart_field.cmp";
				height=24;
				text="Status:";
				ctl_type=dropdown;
				sql="select :text, :tag from /apps/kardia/data/Kardia_DB/_e_collaborator_status/rows order by :text";
				width=110;
				label_width=55;
				numdisplay=10;

				stat_me_hints "widget/hints" { style=notnull; }

				save_on_data_change2_me "widget/connector" { event=DataChange; event_delay=0.05; target=editcollab_me_form; action=Save; }

				discard_on_lose_focus2_me "widget/connector" { event=LoseFocus; event_delay=0.4; target=editcollab_me_form; action=Discard; }
				}
			    }
			}
		    }

		editcollab_detail "widget/table-row-detail"
		    {
		    height=34;
		    width=248;
		    display_for=runclient(:active_collab_osrc:collab_one);

		    editcollab_form "widget/form"
			{
			//allow_obscure=yes;
			confirm_discard=no;

			refresh_profile_on_save2 "widget/connector" { event=DataSaved; target=people_list; action=CollabChanged; }
			refresh_profile_on_delete "widget/connector" { event=DataDeleted; target=people_list; action=CollabChanged; }

			editcollab_hbox "widget/hbox"
			    {
			    x=4; y=10; height=24; width=240;
			    spacing=10;

			    editcollab_meta "widget/component" { path="/apps/kardia/modules/base/record_metadata_hidden.cmp"; visual=no; }

			    editcollab_type_hbox "widget/hbox"
				{
				width=110;
				spacing=5;
				fl_width=10;

				editcollab_label "widget/label"
				    {
				    width=50;
				    fl_width=0;
				    align=right;
				    value=runclient(:active_collab_osrc:collaborator_firstname + ":");
				    }

				editcollab_type "widget/dropdown"
				    {
				    mode=dynamic_server;
				    fieldname=collaborator_type_id;
				    height=24;
				    sql="select :e_collab_type_label, :e_collab_type_id from /apps/kardia/data/Kardia_DB/e_collaborator_type/rows order by :e_collab_type_label";
				    width=55;
				    fl_width=40;
				    numdisplay=10;

				    role_hints "widget/hints" { style=notnull; }

				    save_on_data_change "widget/connector" { event=DataChange; event_delay=0.05; target=editcollab_form; action=Save; }

				    discard_on_lose_focus "widget/connector" { event=LoseFocus; event_delay=0.4; target=editcollab_form; action=Discard; }
				    }
				}

			    editstat_type "widget/component"
				{
				field=collaborator_status;
				path="/sys/cmp/smart_field.cmp";
				height=24;
				text="Status:";
				ctl_type=dropdown;
				sql="select :text, :tag from /apps/kardia/data/Kardia_DB/_e_collaborator_status/rows order by :text";
				width=110;
				fl_width=20;
				label_width=55;
				numdisplay=10;

				stat_hints "widget/hints" { style=notnull; }

				save_on_data_change2 "widget/connector" { event=DataChange; event_delay=0.05; target=editcollab_form; action=Save; }

				discard_on_lose_focus2 "widget/connector" { event=LoseFocus; event_delay=0.4; target=editcollab_form; action=Discard; }
				}
			    }
			}
		    }

		addcollab_detail "widget/table-row-detail"
		    {
		    height=34;
		    width=248;
		    display_for=runclient(not :active_collab_osrc:collab_me);

		    add_btn "widget/textbutton"
			{
			x=50;
			y=8;
			width=140;
			height=24;
			text="Add to My People";
			background="/apps/kardia/images/bg/lsblue_btngradient.png";
			fgcolor1=white;
			fgcolor2=black;
			disable_color="#334466";

			on_add "widget/connector"
			    {
			    event=Click;
			    target=search;
			    action=AddPerson;
			    Origin=add_btn;
			    partner=runclient(:active_collab_osrc:p_partner_key);
			    partnername=runclient(:active_collab_osrc:partner_name);
			    }
			}
		    }
		}
	    }

	customize_pane "widget/pane"
	    {
	    height=36; x=4; width=240;
	    style=bordered;
	    border_radius=8;
	    border_color="#6080c0";

	    customize_hbox "widget/hbox"
		{
		x=6; y=3;
		width=226; height=24;
		spacing=10; x=4;

		customize_label "widget/label" { y=5; style=bold; width=190; value=runclient((condition(isnull(:show_all:value, 'M') = 'M', 'My People', condition(:show_all:value = 'C', 'All', substring(:show_all:label, 11, 99))) + ': ' + isnull(:with_status:label + ': ', 'Active or Priority: ') + condition(not (:only_newpeople:value > 0) and :with_tag1:value is null and :with_tag2:value is null and :in_track:value is null and :in_role:value is null , '', condition(:only_newpeople:value > 0, 'Only New People, ', '') + condition(:with_tag1:value is not null, '' + :with_tag1:label + ', ', '') + condition(:with_tag2:value is not null, '' + :with_tag2:label + ', ', '') + condition(:in_track:value is not null, 'Track ' + :in_track:label + ', ', '') + condition(:in_role:value is not null, 'My Role is ' + :in_role:label + ', ', ''))) - ': ' - ', '); }

		customize_menu "widget/textbutton"
		    {
		    widget_class=crm_iconbutton;
		    height=20; width=30;
		    fl_width=0;
		    text=" ";
		    image="/apps/kardia/images/icons/ionicons-hamburger.svg";
		    image_position=right;
		    image_margin=4;
		    background=null;
		    align=right;
		    image_width=18;
		    image_height=18;

		    on_click "widget/connector"
			{
			event=Click;
			target=customize_win;
			action=ToggleVisibility;
			IsModal=1;
			PointAt=customize_menu;
			PointSide=runclient('bottom');
			}
		    }
		}
	    }

	customize_win "widget/childwindow"
	    {
	    titlebar=no;
	    toplevel=yes;
	    visible=no;
	    x=0; y=0; width=400; height=292;
	    fl_width=100;
	    border_radius=8;
	    border_style=solid;
	    border_color="#6080c0";
	    shadow_radius=8;
	    shadow_color="#6080c0";
	    shadow_offset=0;
	    background=null;
	    bgcolor="#f8f8f8";

	    customize_vbox "widget/vbox"
		{
		x=20; y=10; width=358; height=270;
		spacing=8;

		customize_form "widget/form"
		    {
		    allow_nodata=no;
		    allow_new=yes;
		    allow_query=no;
		    allow_view=no;
		    allow_modify=no;
		    allow_obscure=yes;
		    //confirm_discard=no;

		    search_on_change "widget/connector"
			{
			event=DataChange;
			target=active_collab_osrc;
			action=QueryParam;
			search_newonly=runclient(:only_newpeople:value);
			search_tag1=runclient(:with_tag1:value);
			search_tag2=runclient(:with_tag2:value);
			search_track=runclient(:in_track:value);
			search_role=runclient(:in_role:value);
			search_stat=runclient(:with_status:value);
			search_all=runclient(:show_all:value);
			}

		    heading "widget/label"
			{
			height=24;
			font_size=15;
			style=bold;
			align=center;
			text="Search Within People:";
			}

		    show_all "widget/component"
			{
			height=20;
			label_width=70;
			path="/sys/cmp/smart_field.cmp";
			ctl_type=dropdown;
			text="Show:";
			numdisplay=12;
			fieldname=search_all;
			sql = " select label='My People', value='M', sel=1;
				select label='All CRM People', value='C', sel=0;
				select
				    label = 'People For ' + condition(char_length(isnull(:p:p_org_name,'')) > 1, :p:p_org_name, condition(char_length(isnull(:p:p_preferred_name,'')) > 1, :p:p_preferred_name, :p:p_given_name) + ' ' + :p:p_surname),
				    value = :s:p_partner_key
				from
				    /apps/kardia/data/Kardia_DB/s_sec_endorsement/rows se,
				    /apps/kardia/data/Kardia_DB/p_partner/rows p,
				    /apps/kardia/data/Kardia_DB/p_staff/rows s
				where
				    :se:s_endorsement = 'kardia:crm' and
				    :se:s_context = 'kardia' and
				    :s:p_kardia_login != user_name() and
				    substring(:se:s_subject, 3) = :s:p_kardia_login and
				    :s:p_partner_key = :p:p_partner_key and
				    has_endorsement('kardia:crm_manage', 'kardia')
				";
			sra_hints "widget/hints" { style=notnull,default; default=runclient('M'); }
			}

		    only_newpeople "widget/component"
			{
			height=20;
			label_width=70;
			path="/sys/cmp/smart_field.cmp";
			ctl_type=checkbox;
			text="Only New?";
			fieldname=search_newonly;

			only_new_hints "widget/hints" { style=notnull; default=0; }
			}

		    with_tag1 "widget/component"
			{
			height=20;
			label_width=70;
			path="/sys/cmp/smart_field.cmp";
			ctl_type=dropdown;
			numdisplay=12;
			text="With Tag:";
			fieldname=search_tag1;
			sql = " select
				    :e_tag_label,
				    :e_tag_id
				from
				    /apps/kardia/data/Kardia_DB/e_tag_type/rows
				order by
				    :e_tag_label
				";
			}

		    with_tag2 "widget/component"
			{
			height=20;
			label_width=70;
			path="/sys/cmp/smart_field.cmp";
			ctl_type=dropdown;
			numdisplay=12;
			text="With Tag:";
			fieldname=search_tag2;
			sql = " select
				    :e_tag_label,
				    :e_tag_id
				from
				    /apps/kardia/data/Kardia_DB/e_tag_type/rows
				order by
				    :e_tag_label
				";
			}

		    in_track "widget/component"
			{
			height=20;
			label_width=70;
			path="/sys/cmp/smart_field.cmp";
			ctl_type=dropdown;
			numdisplay=12;
			text="In Track:";
			fieldname=search_track;
			sql = " select
				    :e_track_name,
				    :e_track_id
				from
				    /apps/kardia/data/Kardia_DB/e_engagement_track/rows
				order by
				    :e_track_name
				";
			}

		    in_role "widget/component"
			{
			height=20;
			label_width=70;
			path="/sys/cmp/smart_field.cmp";
			ctl_type=dropdown;
			numdisplay=12;
			text="My Role:";
			fieldname=search_role;
			sql = " select
				    :e_collab_type_label,
				    :e_collab_type_id
				from
				    /apps/kardia/data/Kardia_DB/e_collaborator_type/rows
				order by
				    :e_collab_type_label
				";
			}

		    with_status "widget/component"
			{
			height=20;
			label_width=70;
			path="/sys/cmp/smart_field.cmp";
			ctl_type=dropdown;
			text="Status:";
			fieldname=search_stat;
			sql = " select label='Active or Priority', value='AP', sel=1;
				select label='Priority', value='P', sel=0;
				select label='Active, Priority, or Inactive', value='API', sel=0;
				select label='Only Inactive', value='I', sel=0
				";
			ws_hints "widget/hints" { style=notnull,default; default=runclient('AP'); }
			}

		    sep1 "widget/autolayoutspacer" { height=1; }

		    role_select_dd "widget/dropdown"
			{
			condition = 0;
			height=24;
			mode=dynamic_server;
			numdisplay=24;
			sql = " select
				    'All of My People',
				    'A',
				    1
				;

				select
				    'New People Only',
				    'N',
				    0
				;

				select
				    'My Role: ' + :e_collab_type_label,
				    'R' + convert(string,:e_collab_type_id),
				    0
				from
				    /apps/kardia/data/Kardia_DB/e_collaborator_type/rows
				order by
				    :e_collab_type_label
				;

				select
				    'Track: ' + :e_track_name,
				    'T' + convert(string,:e_track_id),
				    0
				from
				    /apps/kardia/data/Kardia_DB/e_engagement_track/rows
				order by
				    :e_track_name
				;

				select
				    'Tag: ' + :e_tag_label,
				    'G' + convert(string,:e_tag_id),
				    0
				from
				    /apps/kardia/data/Kardia_DB/e_tag_type/rows
				order by
				    :e_tag_label
				";

			role_dd_hints "widget/hints" { style=notnull; }

			on_role_select_all "widget/connector"
			    {
			    event=DataChange;
			    event_condition=runclient(substring(:Value,1,1) == 'A');
			    target=active_collab_osrc;
			    action=QueryParam;
			    }
			on_role_select_new "widget/connector"
			    {
			    event=DataChange;
			    event_condition=runclient(substring(:Value,1,1) == 'N');
			    target=active_collab_osrc;
			    action=QueryParam;
			    is_new=1;
			    }
			on_role_select_role "widget/connector"
			    {
			    event=DataChange;
			    event_condition=runclient(substring(:Value,1,1) == 'R');
			    target=active_collab_osrc;
			    action=QueryParam;
			    role_id=runclient(convert(integer,substring(:Value,2)));
			    }
			on_role_select_track "widget/connector"
			    {
			    event=DataChange;
			    event_condition=runclient(substring(:Value,1,1) == 'T');
			    target=active_collab_osrc;
			    action=QueryText;
			    query=runclient('X' + substring(:Value,2) + 'X');
			    field_list="*track_ids*";
			    }
			on_role_select_tag "widget/connector"
			    {
			    event=DataChange;
			    event_condition=runclient(substring(:Value,1,1) == 'G');
			    target=active_collab_osrc;
			    action=QueryText;
			    query=runclient('X' + substring(:Value,2) + 'X');
			    field_list="*tag_ids*";
			    }

			on_select_close "widget/connector"
			    {
			    event=DataChange;
			    target=customize_win;
			    action=Close;
			    }
			}
		    }

		buttons_hbox "widget/hbox"
		    {
		    height=24; align=center; spacing=10;

		    everyone_btn "widget/textbutton"
			{
			text = "Reset";
			height=24; width=130;
			background="/apps/kardia/images/bg/lsblue_btngradient.png";
			fgcolor1=white;
			fgcolor2=black;
			disable_color="#334466";

			on_everyone_1 "widget/connector"
			    {
			    event=Click;
			    target=active_collab_osrc;
			    action=QueryParam;
			    }
			on_everyone_2 "widget/connector"
			    {
			    condition=0;
			    event=Click;
			    target=customize_win;
			    action=Close;
			    }
			on_everyone_3 "widget/connector"
			    {
			    event=Click;
			    target=customize_form;
			    action=Discard;
			    }
			}

		    cancel_btn "widget/textbutton"
			{
			text = "Close";
			height=24; width=130;
			background="/apps/kardia/images/bg/lsblue_btngradient.png";
			fgcolor1=white;
			fgcolor2=black;
			disable_color="#334466";

			on_cancel "widget/connector" { event=Click; target=customize_win; action=Close; }
			}
		    }
		}
	    }
	}
    }
