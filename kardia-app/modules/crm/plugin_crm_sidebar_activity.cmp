$Version=2$
activity_list "widget/component-decl"
    {
    height=417; width=248;

    title = "Activity";
    icon = "/apps/kardia/images/icons/openiconic-clock.svg";
    sequence = 40;

    Refresh "widget/component-decl-action" { }
    RefreshTasks "widget/component-decl-action" { }
    PartnerSelected "widget/component-decl-event" { }
    SetNumber "widget/component-decl-event" { }
    AllActivity "widget/component-decl-event" { }

    prev_lastrecord "widget/variable" { type=integer; }
    prev_source "widget/variable" { type=string; }

    refresh_timer "widget/timer"
	{
	auto_start=0;
	auto_reset=1;
	msec=60000;

	on_refresh "widget/connector" { event=Expire; target=activity_osrc; action=Refresh; }
	}

    activity_osrc "widget/osrc"
	{
	sql = "	select
		    :activity_group_id,
		    new_cnt = ( select
				    count(1)
				from
				    /apps/kardia/data/Kardia_DB/c_member/rows m1,
				    /apps/kardia/data/Kardia_DB/c_message/rows msg
				where
				    :m1:s_username = user_name() and
				    :msg:c_chat_id = :m1:c_chat_id and
				    isnull(:m1:c_last_read_message_id,0) < :msg:c_message_id
				)
		from
		    /apps/kardia/modules/crm/activity.qy
		";
	autoquery=onload;
	replicasize=2;
	readahead=2;
	indicates_activity=no;

	//on_endquery "widget/connector" { event=EndQuery; target=activity_data_osrc; action=QueryParam; e_activity_group_id=runclient(:activity_osrc:activity_group_id); }
	on_endquery "widget/connector" { event=EndQuery; target=activity_data_osrc; action=Refresh; }
	on_endquery_start_timer "widget/connector" { event=EndQuery; target=refresh_timer; action=SetTimer; Time=30000; AutoReset=1; }

	set_cnt_on_load "widget/connector"
	    {
	    event=EndQuery;
	    target=activity_list;
	    action=SetNumber;
	    Which=runclient("Activity");
	    Number=runclient(:activity_osrc:new_cnt);
	    Color=runclient(condition(:activity_osrc:new_cnt > 0, 'red', 'black'));
	    }
	}

    activity_vbox "widget/vbox"
	{
	x=2; y=2; width=244; height=413;
	spacing=8;

	activity_data_osrc "widget/osrc"
	    {
	    activity_id "widget/parameter" { type=integer; default=runclient(:activity_osrc:activity_group_id); }
	    sql = " select
			:e:p_partner_key,
			name = :e:e_reference_info,
			:e:e_activity_date,
			:e:e_activity_type,
			:e:e_reference_info,
			:e:e_info,
			info_1 = substring(:e:e_info, 1, charindex('\n', :e:e_info) - 1),
			info_2 = substring(:e:e_info, charindex('\n', :e:e_info) + 1),
			shortdate = substring(convert(string,:e:e_activity_date),1,11),
			textdate = condition(datediff(minute,:e:e_activity_date,getdate()) = 0, 'now',
				    condition(datediff(minute,:e:e_activity_date,getdate()) = 1, '' + 'a minute ago', 
				    condition(datediff(minute,:e:e_activity_date,getdate()) < 60, '' + datediff(minute,:e:e_activity_date,getdate()) + ' minutes ago', 
				    condition(datediff(minute,:e:e_activity_date,getdate()) < 120, 'an hour ago', 
				    condition(datediff(minute,:e:e_activity_date,getdate()) < 720, '' + (datediff(minute,:e:e_activity_date,getdate()) / 60) + ' hours ago', 
				    condition(datediff(day,:e:e_activity_date,getdate()) = 1, 'yesterday', 
				    condition(datediff(minute,:e:e_activity_date,getdate()) < 1440, '' + (datediff(minute,:e:e_activity_date,getdate()) / 60) + ' hours ago', 
				    condition(datediff(day,:e:e_activity_date,getdate()) < 7, '' + datediff(day,:e:e_activity_date,getdate()) + ' days ago', 

				    substring(convert(string,:e:e_activity_date),1,11)
				    )))))))),
			img = isnull( ( select
				    path = :d:e_current_folder + '/' + :d:e_current_filename
				from
				    /apps/kardia/data/Kardia_DB/e_partner_document/rows pd,
				    /apps/kardia/data/Kardia_DB/e_document/rows d,
				    /apps/kardia/data/Kardia_DB/e_document_type/rows dt
				where
				    :pd:p_partner_key = :e:p_partner_key and
				    :pd:e_document_id = :d:e_document_id and
				    :d:e_doc_type_id = :dt:e_doc_type_id and
				    :dt:e_doc_type_label = 'Profile Photo'
				order by
				    :pd:s_date_modified desc
				limit
				    1
				), '/apps/kardia/images/artwork/persona.png')
		    from
			/apps/kardia/data/Kardia_DB/e_activity/rows e
		    order by
			:e:e_activity_date desc
		    where
			:e:e_activity_type != 'HEAD' and
			:e:e_activity_group_id = :parameters:activity_id and
			:e:s_created_by = user_name()
		    ";
	    autoquery=never;
	    readahead=50;
	    replicasize=50;
	    indicates_activity=no;

	    activity_table "widget/table"
		{
		height=381;
		min_rowheight=16;
		max_rowheight=72;
		allow_selection = yes;
		show_selection = yes;
		initial_selection = no;
		demand_scrollbar = yes;
		overlap_scrollbar = yes;
		titlebar = no;
		colsep = 0;
		row_border_radius=6;
		inner_padding = 2;
		cellvspacing = 4;
		rowhighlight_shadow_color = runclient(condition(:activity_data_osrc:e_activity_type == 'USER', '#6080c0', '#f8f8f8'));
		rowhighlight_shadow_location = 'inside';
		rowhighlight_shadow_radius = runclient(condition(:activity_data_osrc:e_activity_type == 'USER', 6, 0));
		rowhighlight_bgcolor = runclient(condition(:activity_data_osrc:e_activity_type == 'USER', "#f0f0f0", "#fff090"));
		textcolorhighlight = runclient(condition(:activity_data_osrc:e_activity_type == 'USER', 'black', "#000000"));
		row1_bgcolor = runclient(condition(:activity_data_osrc:e_activity_type == 'USER', "#f0f0f0", "#f8f8f8"));
		row2_bgcolor = runclient(condition(:activity_data_osrc:e_activity_type == 'USER', "#f0f0f0", "#f8f8f8"));
		//row1_bgcolor = runclient("#f8f8f8");
		//row2_bgcolor = runclient("#f8f8f8");
		//row1_border_color = runclient(condition(:activity_data_osrc:e_activity_type == 'USER', '#6080c0', '#f8f8f8'));
		//row2_border_color = runclient(condition(:activity_data_osrc:e_activity_type == 'USER', '#6080c0', '#f8f8f8'));

		// Clicking on a single-partner collaboration event
		onclick "widget/connector" { event=Click; event_condition=runclient(:e_activity_type == 'COLL' and charindex(',', :e_reference_info) = 0); target=activity_list; action=PartnerSelected; partner_key=runclient(substring(:e_reference_info, charindex('|',:e_reference_info) + 1)); }
		onclicktrk "widget/connector" { event=Click; event_condition=runclient(:e_activity_type == 'TRK' and charindex(',', :e_reference_info) = 0); target=activity_list; action=PartnerSelected; partner_key=runclient(substring(:e_reference_info, 1, charindex('|',:e_reference_info) - 1)); }
		onclicktask "widget/connector" { event=Click; event_condition=runclient(:e_activity_type == 'TASK' and charindex(',', :e_reference_info) = 0); target=activity_list; action=PartnerSelected; partner_key=runclient(:p_partner_key); }

		// Columns in the activity table
		t_img "widget/table-column" { fieldname="img"; width=40; type=image; image_maxwidth=32; image_maxheight=32; align=center; }
		t_name "widget/table-column"
		    {
		    textcolor=runclient(condition(charindex('(offline)', :activity_data_osrc:info_1) > 0, '#808080', '#000000'));
		    fieldname="info_1";
		    width=202;
		    caption_textcolor=runclient(condition(charindex('(offline)', :activity_data_osrc:info_1) > 0, '#808080', '#000000'));
		    caption_value=runclient(condition(char_length(:activity_data_osrc:info_2) > 1, :activity_data_osrc:info_2 + '\n', '') + :activity_data_osrc:textdate);
		    style=bold;
		    font_size=15;
		    wrap=yes;
		    }

		t_rowdetail_multiple "widget/table-row-detail"
		    {
		    display_for=runclient(:activity_data_osrc:e_activity_type = 'COLL' and charindex(',', :activity_data_osrc:e_reference_info) > 0);
		    height=120;
		    width=244;

		    t_multiple_osrc "widget/osrc"
			{
			personlist "widget/parameter" { type=string; }
			sql = "	select
				    :p:p_partner_key,
				    who = condition(char_length(isnull(:p:p_org_name,'')) > 1, :p:p_org_name, condition(char_length(:p:p_preferred_name) > 1, :p:p_preferred_name, :p:p_given_name) + ' ' + :p:p_surname),
				    img = isnull( ( select
						path = :d:e_current_folder + '/' + :d:e_current_filename
					    from
						/apps/kardia/data/Kardia_DB/e_partner_document/rows pd,
						/apps/kardia/data/Kardia_DB/e_document/rows d,
						/apps/kardia/data/Kardia_DB/e_document_type/rows dt
					    where
						:pd:p_partner_key = :c:p_partner_key and
						:pd:e_document_id = :d:e_document_id and
						:d:e_doc_type_id = :dt:e_doc_type_id and
						:dt:e_doc_type_label = 'Profile Photo'
					    order by
						:pd:s_date_modified desc
					    limit
						1
					    ), '/apps/kardia/images/artwork/persona.png')
				from
				    /apps/kardia/data/Kardia_DB/e_collaborator/rows c,
				    /apps/kardia/data/Kardia_DB/p_partner/rows p
				where
				    charindex(',' + :c:name + ',', ',' + :parameters:personlist + ',') > 0 and
				    :p:p_partner_key = :c:p_partner_key
				";
			replicasize=50;
			readahead=50;

			personlist_link "widget/rule"
			    {
			    ruletype=osrc_relationship;
			    target=activity_data_osrc;
			    key_1=personlist;
			    target_key_1=e_reference_info;
			    revealed_only=yes;
			    }

			t_multiple_table "widget/table"
			    {
			    x=32; y=4; width=192; height=110;
			    fl_x=0;
			    min_rowheight=16;
			    max_rowheight=72;
			    allow_selection = yes;
			    show_selection = yes;
			    initial_selection = no;
			    demand_scrollbar = yes;
			    overlap_scrollbar = yes;
			    titlebar = no;
			    colsep = 0;
			    row_border_radius=4;
			    inner_padding = 1;
			    cellvspacing = 0;
			    row1_bgcolor='#fff090';
			    row2_bgcolor='#fff090';
			    rowhighlight_bgcolor="#fff8c8";
			    textcolorhighlight = "black";
			    rowcache_size=40;

			    multiple_onclick "widget/connector" { event=Click; target=activity_list; action=PartnerSelected; partner_key=runclient(:p_partner_key); }

			    t_m_img "widget/table-column" { fieldname="img"; width=24; type=image; image_maxwidth=16; image_maxheight=16; align=center; }
			    t_m_name "widget/table-column" { fieldname="who"; width=168; style=bold; font_size=12; }
			    }
			}
		    }

		t_rowdetail_chat "widget/table-row-detail"
		    {
		    display_for=runclient(char_length(:activity_data_osrc:info_1) > 1 and :activity_data_osrc:e_reference_info != user_name());
		    height=150;
		    width=244;

		    t_chat_osrc "widget/osrc"
			{
			sql = "	select
				    :c:img,
				    :c:message_txt,
				    :c:date_txt,
				    :c:is_hdr,
				    :c:c_chat_id,
				    :c:is_new,
				    :c:is_not_seen,
				    :c:name,
				    :p:p_partner_key,
				    partner_name = condition(char_length(isnull(:p:p_org_name,'')) > 1, :p:p_org_name, condition(char_length(:p:p_preferred_name) > 1, :p:p_preferred_name, :p:p_given_name) + ' ' + :p:p_surname)
				from
				    identity /apps/kardia/modules/crm/chat.qy c,
				    /apps/kardia/data/Kardia_DB/p_partner/rows p
				where
				    :p:p_partner_key =* condition(charindex('#', :c:message_txt) > 0, convert(string, convert(integer, substring(:c:message_txt, charindex('#', :c:message_txt) + 1))), null)
				";
			baseobj="/apps/kardia/modules/crm/chat.qy";
			autoquery=never;
			readahead=40;
			replicasize=40;
			refresh_interval=10000;

			//chat_change_focus1 "widget/connector" { source=activity_data_osrc; event=DataFocusChanged; event_condition=runclient(:t_rowdetail_chat:is_visible); target=prev_lastrecord; action=SetValue; Value=0; }
			chat_change_focus1 "widget/connector" { source=t_rowdetail_chat; event=Open; target=prev_lastrecord; action=SetValue; Value=0; }
			chat_change_focus2 "widget/connector" { source=t_rowdetail_chat; event=Open; event_condition=runclient(:prev_source:value != "/apps/kardia/modules/crm/chat.qy?focus_user=" + :activity_data_osrc:e_reference_info); target=t_chat_table; action=Clear; }
			//chat_change_focus2 "widget/connector" { source=activity_data_osrc; event=DataFocusChanged; event_condition=runclient(:activity_data_osrc:e_activity_type = 'USER'); action=Clear; }
			//chat_change_focus3 "widget/connector" { source=activity_data_osrc; event=DataFocusChanged; event_condition=runclient(:t_rowdetail_chat:is_visible and :activity_data_osrc:e_activity_type = 'USER'); action=ChangeSource; Source=runclient("/apps/kardia/modules/crm/chat.qy?focus_user=" + :activity_data_osrc:e_reference_info); }
			chat_change_focus3 "widget/connector" { source=t_rowdetail_chat; event=Open; event_condition=runclient(:activity_data_osrc:e_activity_type = 'USER'); action=ChangeSource; Source=runclient("/apps/kardia/modules/crm/chat.qy?focus_user=" + :activity_data_osrc:e_reference_info); }
			chat_change_focus4 "widget/connector" { source=t_rowdetail_chat; event=Open; event_condition=runclient(:activity_data_osrc:e_activity_type = 'USER'); target=prev_source; action=SetValue; Value=runclient("/apps/kardia/modules/crm/chat.qy?focus_user=" + :activity_data_osrc:e_reference_info); }

			scroll_end_on_load "widget/connector" { event_condition=runclient(:LastRecord != isnull(:prev_lastrecord:value,0)); event=EndQuery; target=t_chat_osrc; action=Last; }
			save_last_rec "widget/connector" { event=EndQuery; target=prev_lastrecord; action=SetValue; Value=runclient(:LastRecord); }

			t_chat_table "widget/table"
			    {
			    x=32; y=4; width=192; height=110;
			    fl_x=0;
			    min_rowheight=16;
			    max_rowheight=72;
			    allow_selection = no;
			    show_selection = no;
			    initial_selection = yes;
			    demand_scrollbar = yes;
			    overlap_scrollbar = yes;
			    titlebar = no;
			    colsep = 0;
			    row_border_radius=4;
			    inner_padding = 2;
			    cellvspacing = 0;
			    row1_bgcolor=runclient(condition(:t_chat_osrc:is_new, '#fff8c8', 'white'));
			    row2_bgcolor=runclient(condition(:t_chat_osrc:is_new, '#fff8c8', 'white'));
			    nodata_message="No messages yet.";
			    nodata_message_textcolor="#808080";
			    rowcache_size=40;

			    t_chat_img "widget/table-column" { type=image; width=30; fieldname=img; image_maxheight=22; image_maxwidth=22; align=center; }
			    t_chat_text "widget/table-column" { width=166; fieldname=message_txt; wrap=yes; font_size=10; style=runclient(condition(:t_chat_osrc:is_hdr, 'bold', 'plain')); caption_value=runclient(:t_chat_osrc:date_txt + condition(:t_chat_osrc:is_not_seen, ' (not seen)', '')); caption_font_size=10; caption_style=italic; caption_textcolor="#c0c0c0"; }

			    // Open a partner if we see the partner ID and '#' in the chat message.
			    on_click_ref "widget/connector"
				{
				event=Click;
				event_condition=runclient(:p_partner_key is not null);
				target=activity_list;
				action=PartnerSelected;
				partner_key = runclient(:p_partner_key);
				}
			    }

			t_chat_newosrc "widget/osrc"
			    {
			    sql = "select * from /apps/kardia/data/Kardia_DB/c_message/rows c";
			    baseobj = "/apps/kardia/data/Kardia_DB/c_message/rows";
			    autoquery=never;

			    t_chat_form "widget/form"
				{
				allow_modify=no;
				allow_view=no;
				allow_new=yes;
				allow_query=no;
				allow_obscure=yes;

				new_chatid_osrc "widget/osrc"
				    {
				    new_chatid_whom "widget/parameter" { type=string; }
				    sql = " select
						:c:c_chat_id
					    from
						/apps/kardia/data/Kardia_DB/c_member/rows m1,
						/apps/kardia/data/Kardia_DB/c_member/rows m2,
						/apps/kardia/data/Kardia_DB/c_chat/rows c
					    where
						:m1:s_username = user_name() and
						:m2:s_username = :parameters:new_chatid_whom and
						:c:c_chat_id = :m1:c_chat_id and
						:m1:c_chat_id = :m2:c_chat_id
					    having
						((select count(1) from /apps/kardia/data/Kardia_DB/c_member/rows m3 where :m3:c_chat_id = :this:c_chat_id) = 2 and :parameters:new_chatid_whom != user_name()) or
						((select count(1) from /apps/kardia/data/Kardia_DB/c_member/rows m3 where :m3:c_chat_id = :this:c_chat_id) = 1 and :parameters:new_chatid_whom = user_name())
					    ";
				    readahead=2;
				    replicasize=2;
				    autoquery=never;

				    got_the_id "widget/connector" { event=EndQuery; target=chat_id; action=SetValue; Value=runclient(:new_chatid_osrc:c_chat_id); }
				    }

				on_save_refresh_list "widget/connector" { event=DataSaved; target=t_chat_osrc; action=Refresh; }
				on_save_set_focus "widget/connector" { event=DataSaved; target=t_chat_editbox; action=SetFocus; }

				on_new_get_the_id "widget/connector" { event=New; event_delay=0.1; target=new_chatid_osrc; action=QueryParam; new_chatid_whom=runclient(:activity_data_osrc:e_reference_info); }

				t_chat_editbox "widget/editbox"
				    {
				    x=32; y=118; width=192; height=24;
				    fieldname=c_message;
				    empty_description="type a new message here";
				    }
				chat_id "widget/variable" { fieldname=c_chat_id; chat_id_hints "widget/hints" { default=runclient(:t_chat_osrc:c_chat_id); } }
				chat_new_meta "widget/component" { path="/apps/kardia/modules/base/record_metadata_hidden.cmp"; }
				}
			    }
			}
		    }

		t_rowdetail_status "widget/table-row-detail"
		    {
		    display_for=runclient(char_length(:activity_data_osrc:info_1) > 1 and :activity_data_osrc:e_reference_info = user_name());
		    height=60;
		    width=244;

		    status_vbox "widget/vbox"
			{
			x=10; y=10;
			fl_x=0;
			width=224;
			fl_width=100;
			height=42;
			spacing=4;

			stat_osrc "widget/osrc"
			    {
			    sql = runserver("select * from /apps/kardia/data/Kardia_DB/s_user_data/rows where :s_username = " + quote(user_name()));
			    baseobj = "/apps/kardia/data/Kardia_DB/s_user_data/rows";
			    replicasize=2;
			    readahead=2;
			    autoquery=onfirstreveal;

			    stat_form "widget/form"
				{
				allow_query = no;
				allow_nodata = no;
				confirm_discard = no;
				auto_focus = no;

				save_cn "widget/connector" { event=DataSaved; target=activity_osrc; action=Refresh; }


				status_lbl "widget/label" { height=14; text="Update My Status:"; }
				status_eb "widget/editbox" { height=24; tooltip="What are you doing right now?"; fieldname=s_status; empty_description="What are you doing now?"; }
				}

			    status_who "widget/variable" { fieldname=s_username; status_who_hints "widget/hints" { default=runclient(user_name()); } }

			    status_meta "widget/component" { path="/apps/kardia/modules/base/record_metadata_hidden.cmp"; }
			    }
			}
		    }
		}
	    }

	all_activity_btn "widget/textbutton"
	    {
	    x=45;
	    height=24;
	    width=150;
	    text="Detailed Activity...";
	    background="/apps/kardia/images/bg/lsblue_btngradient.png";
	    fgcolor1=white;
	    fgcolor2=black;
	    disable_color="#334466";

	    on_act "widget/connector" { event=Click; target=activity_list; action=AllActivity; }
	    }
	}
    }

