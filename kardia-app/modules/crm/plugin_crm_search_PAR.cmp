$Version=2$
plugin_crm_search_partners "widget/component-decl"
    {
    stage_label = "Person";
    stage_description_expr = "isnull((select isnull('First/Pref Name contains: ' + :param:given_name + ', ', '') + isnull('Last Name starts with: ' + :param:surname + ', ', '') + isnull('Organization contains: ' + :param:org_name + ', ', '') + isnull('Gender: ' + :param:gender + ', ', '') + isnull('Comment Contains: ' + :param:comment + ', ', '') + isnull('Added After: ' + substring(convert(string,:param:start),1,11) + ', ', '') + isnull('Added Before: ' + substring(convert(string,:param:end),1,11) + ', ', '') + isnull('Added By User: ' + :param:adduser + ', ', '') - ', ' from /apps/kardia/modules/crm/advanced_search_params.qyp param where :param:p_search_id = :ss:p_search_id and :param:p_search_stage_id = :ss:p_search_stage_id), '(click to configure)')";
    stage_type = "PAR";
    icon = "/apps/kardia/images/icons/ionicons-people.svg";
    sequence = 10;
    width=410;
    height=192;

    criteria_osrc "widget/parameter" { type=object; }
    criteria_params_osrc "widget/parameter" { type=object; find_container="widget/osrc"; }
    criteria_params_form "widget/parameter" { type=object; find_container="widget/form"; }

    param_vbox "widget/vbox"
	{
	x=0; y=0;
	width=410; height=192;
	spacing=4;

	fname "widget/component" { path="/sys/cmp/smart_field.cmp"; field=given_name; height=24; width=300; label_width=150; form=criteria_params_form; text="First/Pref Name:"; fname_hints "widget/hints" { style=strnull; } }
	lname "widget/component" { path="/sys/cmp/smart_field.cmp"; field=surname; height=24; width=300; label_width=150; form=criteria_params_form; text="Last Name:"; lname_hints "widget/hints" { style=strnull; } }
	orgname "widget/component" { path="/sys/cmp/smart_field.cmp"; field=org_name; height=24; width=300; label_width=150; form=criteria_params_form; text="Organization:"; oname_hints "widget/hints" { style=strnull; } }
	gender "widget/component" { path="/sys/cmp/smart_field.cmp"; field=gender; height=24; width=250; label_width=150; form=criteria_params_form; text="Gender:"; ctl_type=dropdown; sql="select label=:text, value=:tag from /apps/kardia/data/Kardia_DB/_p_partner_gender/rows"; }
	comm "widget/component" { path="/sys/cmp/smart_field.cmp"; field=comment; height=24; width=300; label_width=150; form=criteria_params_form; text="Comment Contains:"; comm_hints "widget/hints" { style=strnull; } }

	added_dates "widget/hbox"
	    {
	    height=24;
	    spacing=10;
	    startdate "widget/component"
		{
		form=criteria_params_form;
		path="/sys/cmp/smart_field.cmp";
		field=start;
		text="Added Between:";
		width=250;
		label_width=150;
		ctl_type=datetime;
		}
	    enddate "widget/component"
		{
		form=criteria_params_form;
		path="/sys/cmp/smart_field.cmp";
		field=end;
		text="And:";
		width=140;
		label_width=40;
		ctl_type=datetime;
		}
	    }
	
	addedby "widget/component"
	    {
	    form=criteria_params_form;
	    path="/sys/cmp/smart_field.cmp";
	    field=adduser;
	    text="Added By:";
	    width=300;
	    label_width=150;
	    height=24;
	    ctl_type=dropdown;
	    numdisplay=12;
	    sql="select label=:u:description + ' (' + :u:name + ')', value=:u:name from /apps/kardia/data/Users.uxu u, /apps/kardia/data/Kardia_DB/p_staff/rows s where :s:p_kardia_login = :u:name and :s:p_is_staff = 1 order by :u:name";
	    }
	}
    }
