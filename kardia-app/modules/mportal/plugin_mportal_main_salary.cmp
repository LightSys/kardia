$Version=2$
salary "widget/component-decl"
    {
    height=602; width=1172;

    title = "Salary";
    icon = "/apps/kardia/images/icons/openiconic-dollar.svg";
    sequence = 15;

    user_osrc "widget/parameter" { type=object; }

    salary_osrc "widget/osrc"
	{
	staff_login "widget/parameter" { type=string; }
	sql = "	select
		    staff_key = :s:p_partner_key,
		    :cs:a_fund,
		    :cc:a_fund_desc,
		    :pg:a_payroll_group_name,
		    :l:a_ledger_number,
		    :l:a_ledger_desc,
		    :y:a_payroll_id,
		    :pp:a_payroll_period,
		    :pp:a_start_date,
		    :pp:a_end_date,
		    :pp:a_pay_date,
		    :pp:a_payroll_period_desc,
		    payee_key = :p:p_partner_key,
		    payee_name = isnull(:y:a_payee_name, condition(char_length(isnull(:p:p_org_name,'')) > 1, :p:p_org_name, condition(char_length(:p:p_preferred_name) > 1, :p:p_preferred_name, :p:p_given_name) + ' ' + :p:p_surname)),
		    netpay = sum(condition(:pit:a_payroll_item_class_code == 'N', :pi:a_actual_amount, $0)),
		    gross = sum(condition(:pit:a_payroll_item_class_code == 'G', :pi:a_actual_amount, $0)),
		    ded = $0 - (sum(condition(:pit:a_payroll_item_class_code == 'G', :pi:a_actual_amount, $0)) - sum(condition(:pit:a_payroll_item_class_code == 'N', :pi:a_actual_amount, $0))),
		    target = isnull((select :sr:a_target_payroll from /apps/kardia/data/Kardia_DB/a_salary_review/rows sr where :sr:a_ledger_number = :pi:a_ledger_number and :sr:a_payroll_id = :pi:a_payroll_id and :sr:s_date_created <= :pp:a_pay_date order by :sr:s_date_created desc limit 1), '-'),
		    adj = isnull('' + round(100.0*(select :sr:a_percentage from /apps/kardia/data/Kardia_DB/a_salary_review/rows sr where :sr:a_ledger_number = :pi:a_ledger_number and :sr:a_payroll_id = :pi:a_payroll_id and :sr:s_date_created <= :pp:a_pay_date order by :sr:s_date_created desc limit 1),1) + '%', '-'),
		    suptarget = isnull((select :srt:a_target_amount from /apps/kardia/data/Kardia_DB/a_support_review_target/rows srt, /apps/kardia/data/Kardia_DB/a_support_review/rows sr where :sr:a_ledger_number = :pi:a_ledger_number and :sr:a_review_date <= :pp:a_pay_date and :srt:a_ledger_number = :sr:a_ledger_number and :srt:a_review = :sr:a_review and :srt:a_fund = :y:a_fund order by :sr:a_review_date desc limit 1),'-'),
		    support = isnull((select :srt:a_amount from /apps/kardia/data/Kardia_DB/a_support_review_target/rows srt, /apps/kardia/data/Kardia_DB/a_support_review/rows sr where :sr:a_ledger_number = :pi:a_ledger_number and :sr:a_review_date <= :pp:a_pay_date and :srt:a_ledger_number = :sr:a_ledger_number and :srt:a_review = :sr:a_review and :srt:a_fund = :y:a_fund order by :sr:a_review_date desc limit 1),'-'),
		    raised = isnull('' + round(100.0*(select convert(double, :srt:a_amount) / convert(double, :srt:a_target_amount) from /apps/kardia/data/Kardia_DB/a_support_review_target/rows srt, /apps/kardia/data/Kardia_DB/a_support_review/rows sr where :sr:a_ledger_number = :pi:a_ledger_number and :sr:a_review_date <= :pp:a_pay_date and :srt:a_ledger_number = :sr:a_ledger_number and :srt:a_review = :sr:a_review and :srt:a_fund = :y:a_fund order by :sr:a_review_date desc limit 1),1) + '%', '-')
		from
		    /apps/kardia/data/Kardia_DB/p_staff/rows s,
		    /apps/kardia/data/Kardia_DB/a_fund_staff/rows cs,
		    /apps/kardia/data/Kardia_DB/a_ledger/rows l,
		    /apps/kardia/data/Kardia_DB/a_fund/rows cc,
		    /apps/kardia/data/Kardia_DB/a_payroll/rows y,
		    /apps/kardia/data/Kardia_DB/p_partner/rows p,
		    /apps/kardia/data/Kardia_DB/a_payroll_group/rows pg,
		    /apps/kardia/data/Kardia_DB/a_payroll_period/rows pp,
		    /apps/kardia/data/Kardia_DB/a_payroll_item/rows pi,
		    /apps/kardia/data/Kardia_DB/a_payroll_item_type/rows pit
		where
		    :s:p_kardia_login = :parameters:staff_login and
		    :cs:p_staff_partner_key = :s:p_partner_key and
		    :l:a_ledger_number = :cs:a_ledger_number and
		    :cc:a_fund = :cs:a_fund and
		    :cc:a_ledger_number = :cs:a_ledger_number and
		    :y:a_ledger_number = :cs:a_ledger_number and
		    :y:a_fund = :cs:a_fund and
		    :p:p_partner_key = :y:p_payee_partner_key and
		    :pg:a_ledger_number = :y:a_ledger_number and
		    :pg:a_payroll_group_id = :y:a_payroll_group_id and
		    :pi:a_ledger_number = :y:a_ledger_number and
		    :pi:a_payroll_group_id = :y:a_payroll_group_id and
		    :pi:a_payroll_id = :y:a_payroll_id and
		    :pp:a_ledger_number = :pi:a_ledger_number and
		    :pp:a_payroll_group_id = :pi:a_payroll_group_id and
		    :pp:a_payroll_period = :pi:a_period and
		    :pp:a_posted = 1 and
		    datepart(year, :pp:a_pay_date) >= datepart(year, getdate()) - 1 and
		    :pit:a_payroll_item_type_code = :pi:a_payroll_item_type_code and
		    :pit:a_ledger_number = :pi:a_ledger_number
		group by
		    :s:p_kardia_login,
		    :pp:a_payroll_period desc,
		    :y:a_payroll_id
		order by
		    :pp:a_payroll_period desc
		limit
		    100
		";
	readahead=101;
	replicasize=101;

	user_link "widget/rule"
	    {
	    ruletype=osrc_relationship;
	    target=user_osrc;
	    key_1=staff_login;
	    target_key_1=staff_login;
	    revealed_only=yes;
	    }

	salary_table "widget/table"
	    {
	    x=0; y=0;
	    height=602;
	    width=1172;
	    row_border_radius=4;
	    demand_scrollbar = yes;
	    overlap_scrollbar = yes;
	    initial_selection = no;
	    colsep = 0;
	    titlebar = yes;
	    min_rowheight = 16;
	    max_rowheight = 200;
	    cellvspacing = 4;
	    row1_bgcolor = white;
	    row2_bgcolor = white;
	    show_selection = yes;
	    inner_padding = 4;
	    //rowhighlight_bgcolor = "#fff090";
	    textcolorhighlight = "#000000";
	    titlecolor = white;
	    hdr_background = "/apps/kardia/images/bg/lsblue_gradient.png";
	    rowhighlight_border_color="#6080c0";
	    rowhighlight_border_radius=3;
	    rowhighlight_shadow_color="#6080c0";
	    rowhighlight_shadow_radius=2;
	    rowhighlight_shadow_offset=1;
	    rowhighlight_shadow_angle=135;
	    rowhighlight_bgcolor="#faf8ff";
	    nodata_message = runclient(condition(:salary_osrc:cx__pending, "No payroll information found.", "Looking up your payroll for this year and last year..."));

	    st_name "widget/table-column" { title = "Payee"; value=runclient(:salary_osrc:payee_name + ' (' + :salary_osrc:payee_key + ')'); width=290; style='bold'; font_size=16; caption_value=runclient('' + :salary_osrc:a_fund + ' - ' + :salary_osrc:a_fund_desc); }
	    st_period "widget/table-column" { title = "Pay Date"; value=runclient(substring(:salary_osrc:a_pay_date,1,11)); width=160; style='bold'; font_size=16; caption_value=runclient('For: ' + substring(:salary_osrc:a_start_date,1,11) + ' - ' + substring(:salary_osrc:a_end_date,1,11)); }
	    st_suptarget "widget/table-column" { title = "SupTarget"; value=runclient(:salary_osrc:suptarget); width=90; style='bold'; font_size=16; caption_value=runclient('monthly'); align=right; caption_align=right; }
	    st_support "widget/table-column" { title = "Raised"; value=runclient(:salary_osrc:support); width=96; style='bold'; font_size=16; caption_value=runclient(''); align=right; }
	    st_pct "widget/table-column" { title = "(%)"; value=runclient(:salary_osrc:raised); width=70; style='bold'; font_size=16; caption_value=runclient(''); align=left; }
	    st_target "widget/table-column" { title = "SalTarget"; value=runclient(:salary_osrc:target); width=96; style='bold'; font_size=16; caption_value=runclient('2x month'); align=right; caption_align=right; }
	    st_gross "widget/table-column" { title = "Gross"; value=runclient(:salary_osrc:gross); width=96; style='bold'; font_size=16; caption_value=runclient(''); align=right; }
	    st_adj "widget/table-column" { title = "(%)"; value=runclient(:salary_osrc:adj); width=70; style='bold'; font_size=16; caption_value=runclient(''); align=left; }
	    st_ded "widget/table-column" { title = "Deduct."; value=runclient(:salary_osrc:ded); width=96; style='bold'; font_size=16; caption_value=runclient(''); align=right; textcolor=runclient(condition(:salary_osrc:ded is not null, condition(charindex('-', :salary_osrc:ded) > 0, '#c00000', '#000000'), 'white')); align=right; }
	    st_netpay "widget/table-column" { title = "Paycheck"; value=runclient(:salary_osrc:netpay); width=96; style='bold'; font_size=16; caption_value=runclient(''); align=right; }

	    paycheck_detail "widget/table-row-detail"
		{
		display_for=runclient(:salary_osrc:staff_key is not null);
		height=240;
		width=1172;

		paycheck_osrc "widget/osrc"
		    {
		    sql = " select
				:pit:a_desc,
				:pit:a_payroll_item_class_code,
				:pi:a_payroll_item_type_code,
				a_actual_amount = condition(:pit:a_payroll_item_class_code = 'T', $0 - :pi:a_actual_amount, :pi:a_actual_amount)
			    from
				/apps/kardia/data/Kardia_DB/a_payroll_item/rows pi,
				/apps/kardia/data/Kardia_DB/a_payroll_item_type/rows pit
			    where
				:pit:a_payroll_item_type_code = :pi:a_payroll_item_type_code and
				:pit:a_ledger_number = :pi:a_ledger_number
			    order by
				:pit:a_payroll_item_form_sequence
			    ";
		    readahead=16;
		    replicasize=16;

		    paycheck_link "widget/rule"
			{
			ruletype=osrc_relationship;
			target=salary_osrc;
			key_1=a_ledger_number;
			key_2=a_payroll_id;
			key_3=a_period;
			key_objname=pi;
			target_key_1=a_ledger_number;
			target_key_2=a_payroll_id;
			target_key_3=a_payroll_period;
			revealed_only=yes;
			}

		    paycheck_table "widget/table"
			{
			x=200; y=20;
			width=414; height=210;
			row_border_radius=4;
			demand_scrollbar = yes;
			overlap_scrollbar = yes;
			initial_selection = no;
			allow_selection = no;
			colsep = 0;
			titlebar = no;
			min_rowheight = 12;
			max_rowheight = 200;
			cellvspacing = 1;
			row1_bgcolor = "#faf8ff";
			row2_bgcolor = "#faf8ff";
			show_selection = no;
			inner_padding = 1;
			textcolorhighlight = "#000000";
			titlecolor = white;
			hdr_background = "/apps/kardia/images/bg/lsblue_gradient.png";
			nodata_message = runclient(condition(:paycheck_osrc:cx__pending, "No paycheck data.", "Loading paycheck information..."));

			pt_linetype "widget/table-column" { title = ""; fieldname=a_desc; width=300; style=runclient(condition(:paycheck_osrc:a_payroll_item_class_code = 'G' or :paycheck_osrc:a_payroll_item_class_code = 'N', 'bold', 'plain')); }
			pt_amount "widget/table-column" { title = ""; fieldname=a_actual_amount; width=100; align=right; style=runclient(condition(:paycheck_osrc:a_payroll_item_class_code = 'G' or :paycheck_osrc:a_payroll_item_class_code = 'N', 'bold', 'plain')); textcolor=runclient(condition(:paycheck_osrc:a_actual_amount is not null, condition(charindex('-', :paycheck_osrc:a_actual_amount) > 0, '#c00000', '#000000'), 'white')); }
			}
		    }
		}
	    }
	}
    }

