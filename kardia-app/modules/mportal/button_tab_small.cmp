$Version=2$
button_tab "widget/component-decl"
    {
    width=76; height=30;

    text "widget/parameter" { type=string; deploy_to_client=yes; }
    active "widget/parameter" { type=integer; default=0; }

    expose_events_for = btn;

    SetNumber "widget/component-decl-action" { }
    onSetNumber1 "widget/connector"
	{
	event_condition=runclient(:Which == :text:value);
	event=SetNumber; target=num_visib_var; action=SetValue; Value=runclient(condition(char_length(convert(string, :Number)) > 1 or :Number > 0, 'yes', 'no'));
	}
    onSetNumber2 "widget/connector"
	{
	event_condition=runclient(:Which == :text:value);
	event=SetNumber; target=num_color_var; action=SetValue; Value=runclient(isnull(:Color, 'red'));
	}
    onSetNumber3 "widget/connector"
	{
	event_condition=runclient(:Which == :text:value);
	event=SetNumber; target=number_label; action=SetValue; Value=runclient(:Number);
	}
    num_visib_var "widget/variable" { type=string; default='no'; }
    num_color_var "widget/variable" { type=string; default='red'; }

    SetBackground "widget/component-decl-action" { }

    point_onload "widget/connector"
	{
	condition=0;
	//condition=runserver(:this:active);
	event=LoadComplete;
	target=pane;
	action=Point;
	X=38;
	Y=40;
	BorderColor=runclient('transparent');
	FillColor=runclient('#5e77b9');
	}

    on_SetBackground "widget/connector"
	{
	event=SetBackground;
	target=pane;
	action=SetBackground;
	Image=runclient(:Image);
	}
    on_SetBackground2 "widget/connector"
	{
	condition=0;
	event=SetBackground;
	target=pane;
	event_condition=runclient(:Image == '/apps/kardia/images/bg/lsblue_gradient.png');
	action=Point;
	X=38;
	Y=40;
	BorderColor=runclient('transparent');
	FillColor=runclient('#5e77b9');
	}
    on_SetBackground3 "widget/connector"
	{
	event=SetBackground;
	target=pane;
	event_condition=runclient(:Image != '/apps/kardia/images/bg/lsblue_gradient.png');
	action=Point;
	}

    pane "widget/pane"
	{
	x=0; y=0;
	width=76; height=30;
	style=flat;
	background=runserver(condition(:this:active,"/apps/kardia/images/bg/lsblue_gradient.png",null));

	vbox "widget/vbox"
	    {
	    x=0; y=5;
	    width=76; height=16;
	    spacing=0;

	    btn "widget/textbutton"
		{
		width=76;
		height=20;
		fl_width=0;
		border_style=none;
		text=runserver(:this:text);
		fgcolor1=white;
		fgcolor2="#404040";
		disable_color="#334466";
		background=null;
		widget_class=crm_iconbutton;
		}

	    number_label "widget/label"
		{
		height=12;
		font_size=9;
		fgcolor=white;
		bgcolor=runclient(isnull(:num_color_var:value, 'red'));
		border_color=white;
		border_radius=4;
		padding=1;
		style=bold;
		text="";
		visible=runclient(isnull(:num_visib_var:value, 'no'));
		align=center;
		}
	    }
	}
    }
