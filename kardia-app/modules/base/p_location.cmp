$Version=2$
p_location_component "widget/component-decl"
    {
    width=806;height=338;

    //This osrc is a parameter passed in from the outside
    sync_osrc "widget/parameter" { type=object; find_container="widget/osrc"; }

    Sync "widget/component-decl-action" {  }
    OpenForms "widget/component-decl-action" {  }
    OpenForms_cn1 "widget/connector"
        {
        event = "OpenForms";
        target = p_location_window;
        action=SetVisibility; 
	IsVisible=1;
        }

    person_location_osrc "widget/osrc"
        {
        sql="	SELECT
		    :l:p_postal_mode,
		    :l:p_purge_date,
		    :l:p_postal_code,
		    :l:p_date_effective,
		    all=(:l:p_address_1 + condition(char_length(rtrim(:l:p_address_2)) > 0 , ' / ' + :l:p_address_2, '') + condition(char_length(rtrim(:l:p_address_3)) > 0, ' / ' + :l:p_address_3, '')),
		    :l:p_in_care_of,
		    :l:p_address_1,
		    :l:p_address_2,
		    :l:p_address_3,
		    :l:p_city,
		    :l:p_state_province,
		    :l:p_country_code,
		    :l:p_location_type,
		    :l:p_record_status_code,
		    :l:p_partner_key,
		    :l:p_date_good_until,
		    :l:p_certified_date,
		    :l:p_location_comments,
		    :l:p_location_id,
		    :l:p_bulk_postal_code,
		    :l:p_postal_status,
		    :l:s_created_by,
		    :l:s_date_created,
		    :l:s_modified_by,
		    :l:s_date_modified,
		    loc_type_text = :lt:text
		FROM
		    identity /apps/kardia/data/Kardia_DB/p_location/rows l,
		    /apps/kardia/data/Kardia_DB/_p_location_type/rows lt
		WHERE
		    :l:p_location_type = :lt:tag
		ORDER BY
		    :l:p_location_id
		";

        baseobj = "/apps/kardia/data/Kardia_DB/p_location/rows";
        replicasize=12;
        readahead=6;
        autoquery=never;

	loc_sync "widget/rule"
	    {
	    ruletype = "osrc_relationship";

	    target = sync_osrc;
	    is_slave = yes;
	    key_1 = p_partner_key;
	    target_key_1 = p_partner_key;
	    revealed_only = yes;
	    }

	autoload_locations "widget/connector"
	    {
	    event=EndQuery;
	    target=associated_location;
	    action=SetItems;
	    SQL=runclient("
		    select 
			label = :p_location_type + ': ' + isnull(:p_city + ', ','') + isnull(:p_state_province,'') + ' (' + rtrim(isnull(:p_address_1 + ' ','') + isnull(:p_address_2 + ' ','') + isnull(:p_address_3,'')) + ')',
			value = :p_location_id
		    from
			/apps/kardia/data/Kardia_DB/p_location/rows where :p_partner_key = " + quote(:person_location_osrc:p_partner_key) + "
		    ;
		    select
			label = :tag + ': (' + :text + ', no address)',
			value = :tag
		    from
			/apps/kardia/data/Kardia_DB/_p_location_type/rows
		    ");
	    }

	// Vbox containing (1) location data and (2) contact data
	location_vbox "widget/vbox"
	    {
	    x=0; y=0;
	    width=806;height=338;
	    spacing=10;

	    // Hbox containing (1) location label and list, and (2) location form
	    location_hbox "widget/hbox"
		{
		height=215;
		spacing=10;

		// Vbox containing (1) location label and (2) location list
		location_list_vbox "widget/vbox"
		    {
		    width=260;
		    spacing=10;

		    location_addr_label "widget/component"
			{
			height=75;
			path = "/apps/kardia/modules/base/address_label.cmp";
			partner = sync_osrc;
			location = person_location_osrc;
			}

		    location_table_pane "widget/pane"
			{
			height=130;
			widget_class=table_bgnd;

			p_location_table "widget/table"
			    {
			    x=0;y=0;
			    height=128;
			    width=258;
			    mode=dynamicrow;

			    table_location_type "widget/table-column" { width=42; title = "Type"; fieldname = loc_type_text; }
			    table_address "widget/table-column" { width=105; title = "Address"; fieldname = all; }
			    table_city "widget/table-column" { width=70; title = "City"; fieldname = p_city; }
			    table_state "widget/table-column" { width=25; title = "St."; fieldname = p_state_province; }
			    //table_postal "widget/table-column" { width=60; title = "Postal"; fieldname = p_postal_code; }
			    }
			}
		    }

		// Vbox containing the location form
		location_form_vbox "widget/vbox"
		    {
		    width=536;
		    spacing=6;

		    location_form "widget/form"
			{
			location_hdr "widget/component"
			    {
			    height = 22;
			    path = "/apps/kardia/modules/base/section_header.cmp";
			    text = "Addresses:";
			    }

			location_form_hbox "widget/hbox"
			    {
			    height=215;
			    spacing=6;

			    location_form_vbox1 "widget/vbox"
				{
				width=300;
				spacing = 4;
				cellsize = 20;

				p_location_type_cmp "widget/component" { width=200; path="/sys/cmp/smart_field.cmp"; field=p_location_type; text="Location Type:"; ctl_type="dropdown"; tooltip="H=home, W=work, S=school, V=vacation"; sql="select :text, :tag from /apps/kardia/data/Kardia_DB/_p_location_type/rows";}
				p_in_care_of_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_in_care_of; text="In Care Of:"; ctl_type="editbox"; tooltip="In Care Of Whom?"; }
				p_address_1_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_address_1; text="Addr Line 1:"; ctl_type="editbox"; tooltip="Address Line 1"; }
				p_address_2_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_address_2; text="Addr Line 2:"; ctl_type="editbox"; tooltip="Address Line 2"; }
				p_address_3_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_address_3; text="Addr Line 3:"; ctl_type="editbox"; tooltip="Address Line 3"; }
				p_city_state_hbox "widget/hbox"
				    {
				    height=20;
				    spacing=4;

				    p_city_cmp "widget/component" { label_width=100; width=201; path="/sys/cmp/smart_field.cmp"; field=p_city; text="City:"; ctl_type="editbox"; tooltip="The city or township"; }
				    p_state_province_cmp "widget/component" { label_width=65; width=95; path="/sys/cmp/smart_field.cmp"; field=p_state_province; text="St/Prov:"; ctl_type="editbox"; tooltip="State or Province"; }
				    }
				p_zip_country_hbox "widget/hbox"
				    {
				    height=20;
				    spacing=4;

				    p_postal_code_cmp "widget/component" { label_width=100; width=196; path="/sys/cmp/smart_field.cmp"; field=p_postal_code; text="Postal/Zip:"; ctl_type="editbox"; tooltip="Zip code or Postal code"; }
				    p_country_code_cmp "widget/component" { label_width=70; width=100; path="/sys/cmp/smart_field.cmp"; field=p_country_code; text="Country:"; ctl_type="editbox"; tooltip="Use ISO codes same as domain names"; }
				    }
				p_location_comments_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_location_comments; text="Comments:"; ctl_type="editbox"; tooltip="Comments about this specific address"; }
				}

			    location_form_vbox2 "widget/vbox"
				{
				width=230;
				spacing=4;
				cellsize=20;

				p_date_effective_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_date_effective; text="Effective:"; ctl_type="datetime"; tooltip="Date when the location becomes effective"; }
				p_date_good_until_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_date_good_until; text="Good Until:"; ctl_type="datetime"; tooltip="Date that this location stopps being effective"; }
				p_purge_date_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_purge_date; text="Purge On:"; ctl_type="datetime"; tooltip="When this address should be purged (deleted) from the database"; }

				p_postal_mode_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_postal_mode; text="Postal Mode:"; ctl_type="editbox"; tooltip="B-Bulk F-FirstClass Used to override postal modes on mailings"; }
				p_bulk_postal_code_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_bulk_postal_code; text="Bulk Post Code:"; ctl_type="editbox"; tooltip="USPS - zip center 3 digits"; }
				p_postal_status_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_postal_status; text="Postal Status:"; tooltip="e.g., ACS status, CASS status K=Addressee-UnKnown F=Forwarding-Order-Expired N=No-such-#-or-Address U=Undeliverable-by-P.O. X=Other"; ctl_type="dropdown"; sql="select :text, :tag from /apps/kardia/data/Kardia_DB/_p_postal_status/rows";}
				p_certified_date_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_certified_date; text="Certified On:"; type="readonly"; ctl_type="datetime"; tooltip="e.g., CASS certification of address"; }
				}
			    }
			}
		    }
		}

	    sep_line "widget/pane" { height=2; style=lowered; }

	    // Hbox containing (1) contact list and (2) contact form
	    contact_hbox "widget/hbox"
		{
		height=101;
		spacing=10;

		p_contact_info_osrc "widget/osrc"
		    {
		    sql="   SELECT
				:c:p_contact_id,
				:c:p_contact_type,
				all=(:c:p_phone_country + ' ' + :c:p_phone_area_city + ' ' + :c:p_contact_data),
				:c:p_phone_country,
				:c:p_contact_comments,
				:c:p_partner_key,
				:c:p_location_id,
				:c:p_record_status_code,
				:c:p_phone_area_city,
				:c:p_contact_data,
				:c:s_created_by,
				:c:s_date_created,
				:c:s_modified_by,
				:c:s_date_modified,
				con_type_txt = :ct:text,
				loc_type_txt = isnull(:lt:text, isnull(:lt2:text,'-'))
			    FROM
				IDENTITY /apps/kardia/data/Kardia_DB/p_contact_info/rows c,
				/apps/kardia/data/Kardia_DB/_p_contact_type/rows ct,
				/apps/kardia/data/Kardia_DB/p_location/rows l,
				/apps/kardia/data/Kardia_DB/_p_location_type/rows lt,
				/apps/kardia/data/Kardia_DB/_p_location_type/rows lt2
			    WHERE
				:c:p_contact_type = :ct:tag and
				convert(integer,:c:p_location_id) *= :l:p_location_id and
				:c:p_partner_key *= :l:p_partner_key and
				:l:p_location_type = :lt:tag and
				convert(string,:c:p_location_id) *= :lt2:tag
			    ORDER BY
				:c:p_location_id,
				:c:p_contact_id
			    ";
		    baseobj = "/apps/kardia/data/Kardia_DB/p_contact_info/rows";
		    replicasize=12;
		    readahead=6;
		    autoquery=never;

		    contact_sync "widget/rule"
			{
			ruletype = "osrc_relationship";

			target = person_location_osrc;
			is_slave = yes;
			key_1 = p_partner_key;
			target_key_1 = p_partner_key;
			revealed_only = yes;
			}

		    contact_table_pane "widget/pane"
			{
			width=260;
			widget_class=table_bgnd;
			p_contact_table "widget/table"
			    {
			    x=0;y=0;
			    height=99;
			    width=258;
			    mode=dynamicrow;

			    table_contact_type "widget/table-column" { width=47; title = "Type"; fieldname = con_type_txt; }
			    table_contact_location_type "widget/table-column" { width=47; title = "Where"; fieldname = loc_type_txt; }
			    table_contact_data "widget/table-column" { width=148; title = "Contact Info"; fieldname = all; }
			    }
			}

		    contact_form_vbox "widget/vbox"
			{
			width=536;
			spacing=6;

			contact_form "widget/form"
			    {
			    contact_hdr "widget/component"
				{
				height = 22;
				path = "/apps/kardia/modules/base/section_header.cmp";
				text = "Contact Information:";
				}

			    contact_form_hbox "widget/hbox"
				{
				height = 73;
				spacing=6;

				contact_form_vbox1 "widget/vbox"
				    {
				    width = 536;
				    cellsize = 20;
				    spacing = 4;

				    contact_type_loc_hbox "widget/hbox"
					{
					spacing = 4;

					p_contact_type_cmp "widget/component"
					    {
					    width=200;
					    path="/sys/cmp/smart_field.cmp";
					    field=p_contact_type;
					    text="Contact Type:";
					    ctl_type="dropdown";
					    tooltip="P=phone, F=fax, C=cell, E=email, W=website"; sql="select :text, :tag from /apps/kardia/data/Kardia_DB/_p_contact_type/rows";

					    enab_disab_phones_1 "widget/connector"
						{
						event = DataChange;
						event_condition = runclient(:Value = 'P' or :Value = 'C' or :Value = 'F');
						target = p_phone_country_cmp;
						action = Enable;
						}
					    enab_disab_phones_2 "widget/connector"
						{
						event = DataChange;
						event_condition = runclient(:Value = 'P' or :Value = 'C' or :Value = 'F');
						target = p_phone_area_city_cmp;
						action = Enable;
						}
					    enab_disab_phones_3 "widget/connector"
						{
						event = DataChange;
						event_condition = runclient(not (:Value = 'P' or :Value = 'C' or :Value = 'F'));
						target = p_phone_country_cmp;
						action = Disable;
						}
					    enab_disab_phones_4 "widget/connector"
						{
						event = DataChange;
						event_condition = runclient(not (:Value = 'P' or :Value = 'C' or :Value = 'F'));
						target = p_phone_area_city_cmp;
						action = Disable;
						}
					    }

					associated_location "widget/component" { width=332; path="/sys/cmp/smart_field.cmp"; field='p_location_id'; ctl_type=dropdown; text='Assoc. Addr:'; sql=runserver("select 1,1 where 1 = 0"); } 
					}

				    p_contact_data_hbox "widget/hbox"
					{
					spacing = 4;
					p_phone_country_cmp "widget/component" { width=140; path="/sys/cmp/smart_field.cmp"; field=p_phone_country; text="Country Code:"; ctl_type="editbox"; tooltip=""; }
					p_phone_area_city_cmp "widget/component" { label_width=80; width=120; path="/sys/cmp/smart_field.cmp"; field=p_phone_area_city; text="Area Code:"; ctl_type="editbox"; tooltip=""; }
					p_contact_data_label "widget/label" { width=65; value=runclient(isnull(condition(:p_contact_type_cmp:value = 'P' or :p_contact_type_cmp:value = 'F' or :p_contact_type_cmp:value = 'C','Phone:',condition(:p_contact_type_cmp:value = 'E','Email:',condition(:p_contact_type_cmp:value = 'B' or :p_contact_type_cmp:value = 'W', 'URL:', 'Contact:'))),'Contact:')); align=right; }
					p_contact_data_eb "widget/editbox" { width=199; fieldname=p_contact_data; }
					}

				    p_contact_comments_cmp "widget/component" { width=536; path="/sys/cmp/smart_field.cmp"; field=p_contact_comments; text="Comments:"; ctl_type="editbox"; tooltip=""; }
				    }
				}
			    }
			}
		    }
		}
	    }

	    // Fields not yet addressed in new version:

		    // location
                    //p_record_status_code_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_record_status_code; text="record status"; form=p_location_form; tooltip="A=active, O=obsolete"; ctl_type="dropdown"; sql="select :text, :tag from /apps/kardia/data/Kardia_DB/_p_record_status/rows";}
                    //s_date_created_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=s_date_created; text="date created"; ctl_type="editbox"; type="create"; form=p_location_form; tooltip="Date this record was created"; }
                    //s_created_by_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=s_created_by; text="created by"; ctl_type="editbox"; type="create"; form=p_location_form; tooltip="User who created this"; }
                    //s_date_modified_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=s_date_modified; text="date modified"; type="modify"; ctl_type="editbox"; form=p_location_form; tooltip="Date this was last modified"; }
                    //s_modified_by_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=s_modified_by; text="modified by"; type="modify"; ctl_type="editbox"; form=p_location_form; tooltip="The user who last modified this record"; }

		    // contact
                    //p_record_status_code_contact_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_record_status_code; text="record status code"; ctl_type="dropdown"; form=p_contact_info_form; tooltip="A=active, O=obsolete"; sql="select :text, :tag from /apps/kardia/data/Kardia_DB/_p_record_status/rows";}
                    //s_contact_date_created_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=s_date_created; text="date created"; type="create"; ctl_type="editbox"; form=p_contact_info_form; tooltip=""; }
                    //s_contact_created_by_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=s_created_by; text="created by"; type="create"; ctl_type="editbox"; form=p_contact_info_form; tooltip=""; }
                    //s_contact_date_modified_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=s_date_modified; type="modify"; text="date modified"; ctl_type="editbox"; form=p_contact_info_form; tooltip=""; }
                    //s_contact_modified_by_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=s_modified_by; type="modify"; text="modified by"; ctl_type="editbox"; form=p_contact_info_form; tooltip=""; }

        }
    }
