$Version=2$
section_header "widget/component-decl"
    {
    height=22;
    width=300;

    text "widget/parameter" { type=string; default=''; deploy_to_client=yes; }
    background "widget/parameter" { type=string; deploy_to_client=yes; }
    form "widget/parameter" { type=object; find_container="widget/form"; }
    object_name "widget/parameter" { type=string; default="Item"; }
    allow_new "widget/parameter" { type=integer; default=0; }
    custom_new "widget/parameter" { type=integer; default=0; }
    allow_delete "widget/parameter" { type=integer; default=0; }
    allow_save "widget/parameter" { type=integer; default=1; }
    allow_cancel "widget/parameter" { type=integer; default=1; }

    New "widget/component-decl-event" { }

    lbl_pane "widget/pane"
	{
	x=0; y=0;
	height=22; width=300;
	widget_class="label";
	border_color = "#153f5f";
	style=flat;
	shadow_radius = 4;
	shadow_offset = 2;
	shadow_color = "#808080";
	shadow_angle = 135;
	background = runserver(isnull(:this:background, "/apps/kardia/images/bg/blue_gradient2.png"));

	lbl "widget/label"
	    {
	    x=4;y=1;
	    width=runserver(300 - 4 - 22*(condition(:this:allow_new,1,0) + condition(:this:allow_delete,1,0) + condition(:this:allow_save,1,0) + condition(:this:allow_cancel,1,0) + condition(:this:custom_new,1,0)));
	    height=18;
	    widget_class="label";
	    fl_width=100;
	    fl_height=100;
	    align=left;
	    valign=middle;
	    value=runclient(:text:value);
	    }

	lbl_hbox "widget/hbox"
	    {
	    x=runserver(300 - 12 - 22*(condition(:this:allow_new,1,0) + condition(:this:allow_delete,1,0) + condition(:this:allow_save,1,0) + condition(:this:allow_cancel,1,0) + condition(:this:custom_new,1,0)) + 10);
	    y=2;
	    width=runserver(22*(condition(:this:allow_new,1,0) + condition(:this:allow_delete,1,0) + condition(:this:allow_save,1,0) + condition(:this:allow_cancel,1,0) + condition(:this:custom_new,1,0)));
	    height=18;
	    spacing=4; cellsize=18;
	    align=right;

	    customnew "widget/component"
		{
		condition = runserver(:this:custom_new != 0);
		path = "/sys/cmp/custom_new_btn.cmp";
		width=18; height=18; fl_width=0;
		object_name="Address/Contact/Info";
		cnNew "widget/connector" { event="Click"; target=section_header; action="New"; }
		}
	    new "widget/component"
		{
		condition = runserver(:this:allow_new != 0);
		width=18; height=18; fl_width=0;
		path = "/sys/cmp/new_btn.cmp";
		form = form;
		object_name = runserver(:this:object_name);
		}
	    delete "widget/component"
		{
		condition = runserver(:this:allow_delete != 0);
		width=18; height=18; fl_width=0;
		path = "/sys/cmp/delete_btn.cmp";
		form = form;
		object_name = runserver(:this:object_name);
		}
	    save "widget/component"
		{
		condition = runserver(:this:allow_save != 0);
		width=18; height=18; fl_width=0;
		path = "/sys/cmp/save_btn.cmp";
		form = form;
		object_name = runserver(:this:object_name);
		}
	    cancel "widget/component"
		{
		condition = runserver(:this:allow_cancel != 0);
		width=18; height=18; fl_width=0;
		path = "/sys/cmp/cancel_btn.cmp";
		form = form;
		object_name = runserver(:this:object_name);
		}
	    }
	}
    }
