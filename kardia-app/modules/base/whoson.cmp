$Version=2$
whoson "widget/component-decl"
    {
    width=282;height=360;

    Stop "widget/component-decl-action" {}
    Stop_cn "widget/connector" { event=Stop; target=whoson_timer; action=CancelTimer; }

    Start "widget/component-decl-action" {}
    Start_cn "widget/connector" { event=Start; target=whoson_timer; action=SetTimer; Time=1; }

    auto_start "widget/parameter" { default=0; type=integer; }

    whoson_timer "widget/timer"
	{
	msec=30000;
	auto_reset=0;
	auto_start=runserver(:this:auto_start);
	interval_cn "widget/connector" { target=whoson_osrc; event=Expire; action=Refresh; }
	}

    whoson_osrc "widget/osrc"
	{
	sql = "select img='/apps/kardia/images/icons/person.gif', :ud:s_status, :ud:s_username, :u:description, :s:name, :s:session_cnt from /sys/cx.sysinfo/users s, /apps/kardia/data/Users.uxu u, /apps/kardia/data/Kardia_DB/s_user_data/rows ud where :s:name = :u:name and :s:name *= :ud:s_username";
	replicasize=100;
	readahead=100;
	autoquery=onfirstreveal;
	endquery_cn "widget/connector" { target=whoson_timer; event=EndQuery; action=SetTimer; Time=30000; }
	indicates_activity = no;

	whoson_tbl "widget/table"
	    {
	    x=0;y=0;width=282;height=300;
	    rowheight = 20;
	    mode=dynamicrow;
	    allow_selection = yes;
	    show_selection = no;
	    demand_scrollbar = yes;
	    overlap_scrollbar = yes;
	    colsep = 0;

	    chat_cn "widget/connector"
		{
		event=DblClick;
		target="chat_cmp"; 
		action="Instantiate"; 
		WithWhom = runclient(:whoson_osrc:name);
		visible = runclient('yes');
		}

	    t_img "widget/table-column" { title=""; fieldname="img"; width=24; type=image; }
	    //t_usr "widget/table-column" { title="User"; fieldname="name"; width=80; }
	    t_desc "widget/table-column" { title="Name"; fieldname="description"; width=98; }
	    t_stat "widget/table-column" { title="Status"; fieldname="s_status"; width=160; }
	    //t_cnt "widget/table-column" { title="Logins"; fieldname="session_cnt"; width=48; }
	    }
	}

    sep "widget/pane" { x=0; y=316; height=2; width=282; style=lowered; fl_height=0; }

    upd_stat "widget/label" { x=0; y=318; height=20; width=282; style=bold; text="Update My Status:"; }

    usrdat_osrc "widget/osrc"
	{
	sql = "select * from /apps/kardia/data/Users.uxu where :name = user_name()";
	replicasize=2;
	readahead=2;
	autoquery=onfirstreveal;
	}

    stat_osrc "widget/osrc"
	{
	sql = "select * from /apps/kardia/data/Kardia_DB/s_user_data/rows";
	baseobj = "/apps/kardia/data/Kardia_DB/s_user_data/rows";
	replicasize=2;
	readahead=2;
	autoquery=never;

        usr_sync "widget/rule"
            {
            ruletype = "osrc_relationship";
            target = usrdat_osrc;
            is_slave = yes;
            key_1 = s_username;
            target_key_1 = name;
            }

	stat_form "widget/form"
	    {
	    allow_query = no;
	    allow_nodata = no;
	    confirm_discard = no;
	    auto_focus = no;

	    save_cn "widget/connector" { event=DataSaved; target=whoson_osrc; action=Refresh; }

	    status_lbl "widget/label" { x=0;y=340;height=20;width=85; value=runclient(isnull(condition(charindex(' ', :usrdat_osrc:description) > 0, substring(:usrdat_osrc:description, 1, charindex(' ', :usrdat_osrc:description) - 1), :usrdat_osrc:description) + ' is:', '')); align=right; }
	    status_eb "widget/editbox" { x=90; y=340; height=20; width=192; tooltip="What are you doing right now?"; fieldname=s_status; empty_description="What are you doing now?"; }

	    stat_dt_c "widget/variable" { fieldname="s_date_created"; stat_dt_c_hints "widget/hints" { default=runclient(getdate()); } }
	    stat_us_c "widget/variable" { fieldname="s_created_by"; stat_us_c_hints "widget/hints" { default=runclient(user_name()); } }
	    stat_dt_m "widget/variable" { fieldname="s_date_modified"; stat_dt_m_hints "widget/hints" { default=runclient(getdate()); style=alwaysdef; } }
	    stat_us_m "widget/variable" { fieldname="s_modified_by"; stat_us_m_hints "widget/hints" { default=runclient(user_name()); style=alwaysdef; } }

	    //f_s_status "widget/component" { x=0; y=340; height=20; width=282; path="/sys/cmp/smart_field.cmp"; field=s_status; text=runserver(condition(charindex(' ', user_name()) > 0, substring(user_name(), 1, charindex(' ', user_name()) - 1), user_name()) + ' is:'); ctl_type="editbox"; tooltip="What are you doing right now?"; }
	    }
	}

    chat_cmp "widget/component"
	{
	path = "/apps/kardia/modules/base/chat_window.cmp";
	multiple_instantiation = yes;
	mode = dynamic;
	toplevel = yes;
	}
    }
