$Version=2$
post_cr_batch "widget/component-decl"
    {
    // Unpost a CR batch - process
    Unpost "widget/component-decl-action" { }
    Unposted "widget/component-decl-event" { }
    unpost_cn "widget/connector"
	{
	event=Unpost;
	target=unpost_osrc_1;
	action=QueryParam;
	a_batch_number = runclient(:a_batch_number);
	a_ledger_number = runclient(:a_ledger_number);
	a_period = runclient(:a_period);
	}

    unpost_osrc_1 "widget/osrc"
	{
	up_a_batch_number "widget/parameter"	{ param_name=a_batch_number; type=integer; }
	up_a_ledger_number "widget/parameter"	{ param_name=a_ledger_number; type=string; }
	up_a_period "widget/parameter"		{ param_name=a_period; type=string; }

	replicasize=1; readahead=1; autoquery=never;
	sql = " UPDATE
			/apps/kardia/data/Kardia_DB/a_subtrx_gift_item/rows g
		SET
			:g:a_posted = 0
		WHERE
			:g:a_batch_number = :parameters:a_batch_number and
			:g:a_ledger_number = :parameters:a_ledger_number and
			:g:a_period = :parameters:a_period
		;

		UPDATE
			/apps/kardia/data/Kardia_DB/a_subtrx_gift_group/rows g
		SET
			:g:a_posted = 0
		WHERE
			:g:a_batch_number = :parameters:a_batch_number and
			:g:a_ledger_number = :parameters:a_ledger_number and
			:g:a_period = :parameters:a_period
		";

	end_unpost "widget/connector"
	    { 
	    event=EndQuery;
	    target=post_cr_batch;
	    action=Unposted;
	    a_batch_number = runclient(:unpost_osrc_1:a_batch_number);
	    a_ledger_number = runclient(:unpost_osrc_1:a_ledger_number);
	    a_period = runclient(:unpost_osrc_1:a_period); 
	    }
	}

    // Post a CR batch - process
    Post "widget/component-decl-action" { }
    Posted "widget/component-decl-event" { }
    post_cn "widget/connector"
	{
	event=Post;
	target=post_osrc_1;
	action=QueryParam;
	a_batch_number = runclient(:a_batch_number);
	a_ledger_number = runclient(:a_ledger_number);
	a_period = runclient(:a_period);
	}

    post_osrc_1 "widget/osrc"
	{
	p_a_batch_number "widget/parameter"	{ param_name=a_batch_number; type=integer; }
	p_a_ledger_number "widget/parameter"	{ param_name=a_ledger_number; type=string; }
	p_a_period "widget/parameter"		{ param_name=a_period; type=string; }

	replicasize=1; readahead=1; autoquery=never;
	sql = " UPDATE
			/apps/kardia/data/Kardia_DB/a_subtrx_gift_item/rows g
		SET
			:g:a_posted = 1
		WHERE
			:g:a_batch_number = :parameters:a_batch_number and
			:g:a_ledger_number = :parameters:a_ledger_number and
			:g:a_period = :parameters:a_period
		;
		UPDATE
			/apps/kardia/data/Kardia_DB/a_subtrx_gift_group/rows g
		SET
			:g:a_posted = 1
		WHERE
			:g:a_batch_number = :parameters:a_batch_number and
			:g:a_ledger_number = :parameters:a_ledger_number and
			:g:a_period = :parameters:a_period
		;

		INSERT INTO
			/apps/kardia/data/Kardia_DB/m_list_membership/rows
		SELECT
			:a:m_list_code,
			p_partner_key = :g:p_donor_partner_id,
			m_num_copies=1,
			m_delivery_method='M',
			m_member_type='M',
			m_membership_status='P',
			m_complimentary=1,
			m_show_contact=1,
			m_reason_member='D',
			m_comments = 'Donated to ' + sum(:i:a_cost_center + ',') - ',',
			s_date_created=getdate(),
			s_created_by=user_name(),
			s_date_modified=getdate(),
			s_modified_by=user_name()
		FROM
			/apps/kardia/data/Kardia_DB/a_subtrx_gift_group/rows g,
			/apps/kardia/data/Kardia_DB/a_subtrx_gift_item/rows i,
			/apps/kardia/data/Kardia_DB/a_cc_auto_subscribe/rows a
		WHERE
			:g:a_period = :parameters:a_period and
			:g:a_ledger_number = :parameters:a_ledger_number and
			:g:a_batch_number = :parameters:a_batch_number and
			:i:a_ledger_number = :g:a_ledger_number and
			:i:a_batch_number = :g:a_batch_number and
			:i:a_gift_number = :g:a_gift_number and
			:a:a_cost_center = :i:a_cost_center and
			:a:a_ledger_number = :i:a_ledger_number
		GROUP BY
			:g:p_donor_partner_id,
			--:i:a_cost_center,
			:a:m_list_code
		HAVING
			(select :m:m_membership_status
			   from /apps/kardia/data/Kardia_DB/m_list_membership/rows m
			  where :m:m_list_code = :this:m_list_code and :m:p_partner_key = :this:p_partner_key and not (:m:m_membership_status = 'D' or (:m:m_membership_status = 'C' and isnull(:m:m_reason_cancel,' ') = 'G'))
			) is null
		;

		--UPDATE
		--	IDENTITY /apps/kardia/data/Kardia_DB/m_list_membership/rows m,
		--	/apps/kardia/data/Kardia_DB/a_subtrx_gift_group/rows g,
		--	/apps/kardia/data/Kardia_DB/a_subtrx_gift_item/rows i,
		--	/apps/kardia/data/Kardia_DB/a_cc_auto_subscribe/rows a
		--SET
		--	:m:m_membership_status = 'P',
		--	:m:m_reason_member = 'D',
		--	:m:m_comments = 'Re-donated to ' + :i:a_cost_center,
		--	:m:s_date_modified = getdate(),
		--	:m:s_modified_by = user_name()
		--WHERE
		--	:g:a_period = :parameters:a_period and
		--	:g:a_ledger_number = :parameters:a_ledger_number and
		--	:g:a_batch_number = :parameters:a_batch_number and
		--	:i:a_ledger_number = :g:a_ledger_number and
		--	:i:a_batch_number = :g:a_batch_number and
		--	:i:a_gift_number = :g:a_gift_number and
		--	:a:a_cost_center = :i:a_cost_center and
		--	:a:a_ledger_number = :i:a_ledger_number and
		--	:m:m_list_code = :a:m_list_code and
		--	:m:p_partner_key = :g:p_donor_partner_id and
		--	(:m:m_membership_status = 'D' or
		--	    (:m:m_membership_status = 'C' and :m:m_reason_cancel = 'G'))
		--;

		UPDATE
		    identity /apps/kardia/data/Kardia_DB/a_subtrx_gift_item/rows i,
		    /apps/kardia/data/Kardia_DB/a_subtrx_gift_group/rows g
		SET
		    :i:p_dn_donor_partner_id = :g:p_donor_partner_id,
		    :i:a_dn_gift_received_date = :g:a_gift_received_date,
		    :i:a_dn_gift_postmark_date = :g:a_gift_postmark_date,
		    :i:a_dn_receipt_number = :g:a_receipt_number,
		    :i:p_dn_ack_partner_id = :g:p_ack_partner_id,
		    :i:p_dn_pass_partner_id = :g:p_pass_partner_id,
		    :i:a_dn_gift_type = :g:a_gift_type
		WHERE
		    :g:a_ledger_number = :parameters:a_ledger_number and
		    :g:a_batch_number = :parameters:a_batch_number and
		    :i:a_ledger_number = :g:a_ledger_number and
		    :i:a_batch_number = :g:a_batch_number and
		    :i:a_gift_number = :g:a_gift_number
		";

	end_post "widget/connector"
	    { 
	    event=EndQuery;
	    target=post_cr_batch;
	    action=Posted;
	    a_batch_number = runclient(:post_osrc_1:a_batch_number);
	    a_ledger_number = runclient(:post_osrc_1:a_ledger_number);
	    a_period = runclient(:post_osrc_1:a_period); 
	    }
	}

    // Set receipt sent date - process
    SetSentDate "widget/component-decl-action" { }
    SetSentDateComplete "widget/component-decl-event" { }
    setdate_cn "widget/connector"
	{
	event=SetSentDate;
	target=setdate_osrc;
	action=QueryParam;
	a_batch_number = runclient(:a_batch_number);
	a_ledger_number = runclient(:a_ledger_number);
	a_period = runclient(:a_period);
	a_receipt_sent_date = runclient(:a_receipt_sent_date);
	types = runclient(:types);
	}
    setdate_osrc "widget/osrc"
	{
	sd_a_batch_number "widget/parameter"	{ param_name=a_batch_number; type=integer; }
	sd_a_ledger_number "widget/parameter"	{ param_name=a_ledger_number; type=string; }
	sd_a_period "widget/parameter"		{ param_name=a_period; type=string; }
	sd_a_receipt_sent_date "widget/parameter" { param_name=a_receipt_sent_date; type=string; }
	sd_types "widget/parameter" { param_name=types; type=string; }

	replicasize=1; readahead=1; autoquery=never;
	sql = " UPDATE
			/apps/kardia/data/Kardia_DB/a_subtrx_gift_group/rows g
		SET
			:g:a_receipt_sent = 1,
			:g:a_receipt_sent_date = :parameters:a_receipt_sent_date
		WHERE
			:g:a_batch_number = :parameters:a_batch_number and
			:g:a_ledger_number = :parameters:a_ledger_number and
			:g:a_period = :parameters:a_period and
			:g:a_receipt_sent = 0 and
			(:parameters:types = '_' or :parameters:types = :g:a_receipt_desired)
		";
	end_setdate "widget/connector"
	    { 
	    event=EndQuery;
	    target=post_cr_batch;
	    action=SetSentDateComplete;
	    a_batch_number = runclient(:setdate_osrc:a_batch_number);
	    a_ledger_number = runclient(:setdate_osrc:a_ledger_number);
	    a_period = runclient(:setdate_osrc:a_period); 
	    }
	}

    // Unpost a CR batch from GL - process
    GLUnpost "widget/component-decl-action" { }
    GLUnposted "widget/component-decl-event" { }
    glunpost_cn "widget/connector"
	{
	event=GLUnpost;
	target=glunpost_osrc_1;
	action=QueryParam;
	a_batch_number = runclient(:a_batch_number);
	a_ledger_number = runclient(:a_ledger_number);
	a_period = runclient(:a_period);
	}

    glunpost_osrc_1 "widget/osrc"
	{
	gup_a_batch_number "widget/parameter"	{ param_name=a_batch_number; type=integer; }
	gup_a_ledger_number "widget/parameter"	{ param_name=a_ledger_number; type=string; }
	gup_a_period "widget/parameter"		{ param_name=a_period; type=string; }

	replicasize=1; readahead=1; autoquery=never;
	sql = "	DELETE
			/apps/kardia/data/Kardia_DB/a_transaction/rows t
		WHERE
			:t:a_batch_number = :parameters:a_batch_number and
			:t:a_origin = 'CR' and
			:t:a_ledger_number = :parameters:a_ledger_number and
			:t:a_period = :parameters:a_period
		;
		UPDATE
			/apps/kardia/data/Kardia_DB/a_subtrx_gift_item/rows g
		SET
			:g:a_posted_to_gl = 0
		WHERE
			:g:a_batch_number = :parameters:a_batch_number and
			:g:a_ledger_number = :parameters:a_ledger_number and
			:g:a_period = :parameters:a_period
		;
		UPDATE
			/apps/kardia/data/Kardia_DB/a_subtrx_gift_group/rows g
		SET
			:g:a_posted_to_gl = 0
		WHERE
			:g:a_batch_number = :parameters:a_batch_number and
			:g:a_ledger_number = :parameters:a_ledger_number and
			:g:a_period = :parameters:a_period
		";

	end_glunpost "widget/connector"
	    { 
	    event=EndQuery;
	    target=post_cr_batch;
	    action=GLUnposted;
	    a_batch_number = runclient(:glunpost_osrc_1:a_batch_number);
	    a_ledger_number = runclient(:glunpost_osrc_1:a_ledger_number);
	    a_period = runclient(:glunpost_osrc_1:a_period); 
	    }
	}

    // Post a CR batch - process
    GLPost "widget/component-decl-action" { }
    GLPosted "widget/component-decl-event" { }
    glpost_cn "widget/connector"
	{ 
	event=GLPost;
	target=glpost_osrc_1;
	action=QueryParam;
	a_batch_number = runclient(:a_batch_number);
	a_ledger_number = runclient(:a_ledger_number);
	a_period = runclient(:a_period);
	a_cash_account = runclient(:a_cash_account);
	a_cash_costctr = runclient(:a_cash_costctr);
	}
    glpost_osrc_1 "widget/osrc"
	{
	glp_a_batch_number "widget/parameter" { param_name=a_batch_number; type=integer; }
	glp_a_ledger_number "widget/parameter" { param_name=a_ledger_number; type=string; }
	glp_a_period "widget/parameter" { param_name=a_period; type=string; }
	glp_a_cash_account "widget/parameter" { param_name=a_cash_account; type=string; }
	glp_a_cash_costctr "widget/parameter" { param_name=a_cash_costctr; type=string; }
	replicasize=1; readahead=1; autoquery=never;


	sql = "	DECLARE OBJECT config;

		-- Get configuration data on account codes we're going to use.
		SELECT
			:config:admin_exp = isnull( (select :a_config_value from /apps/kardia/data/Kardia_DB/a_config/rows ac where :ac:a_ledger_number = :parameters:a_ledger_number and :ac:a_config_name = 'RcptAdminExp'), '5900'),
			:config:admin_rev = isnull( (select :a_config_value from /apps/kardia/data/Kardia_DB/a_config/rows ac where :ac:a_ledger_number = :parameters:a_ledger_number and :ac:a_config_name = 'RcptAdminRev'), '4900'),
			:config:ifa_acct = isnull( (select :a_config_value from /apps/kardia/data/Kardia_DB/a_config/rows ac where :ac:a_ledger_number = :parameters:a_ledger_number and :ac:a_config_name = 'GLIfaAcct'), '1900')
		;

		-- Get configuration data on account classes.
		SELECT
			:config:admin_exp_class = isnull( (select :a_default_category from /apps/kardia/data/Kardia_DB/a_account/rows a where :a:a_ledger_number = :parameters:a_ledger_number and :a:a_account_code = :config:admin_exp), '52'),
			:config:admin_rev_class = isnull( (select :a_default_category from /apps/kardia/data/Kardia_DB/a_account/rows a where :a:a_ledger_number = :parameters:a_ledger_number and :a:a_account_code = :config:admin_rev), '42'),
			:config:cash_class = isnull( (select :a_default_category from /apps/kardia/data/Kardia_DB/a_account/rows a where :a:a_ledger_number = :parameters:a_ledger_number and :a:a_account_code = :parameters:a_cash_account), '10'),
			:config:ifa_class = isnull( (select :a_default_category from /apps/kardia/data/Kardia_DB/a_account/rows a where :a:a_ledger_number = :parameters:a_ledger_number and :a:a_account_code = :config:ifa_acct), '19')
		;
	
		DELETE
			/apps/kardia/data/Kardia_DB/a_transaction_tmp/rows t
		WHERE
			:t:a_ledger_number = :parameters:a_ledger_number and
			:t:a_batch_number = :parameters:a_batch_number
		;

		INSERT INTO
			/apps/kardia/data/Kardia_DB/a_transaction/rows
		SELECT
			:gi:a_ledger_number,
			a_batch_number = :parameters:a_batch_number,
			a_journal_number=1,
			:gi:a_period,
			-- a_effective_date = isnull(:gi:a_gift_postmark_date, :gi:a_gift_received_date),
			a_effective_date = :b:a_default_effective_date,
			a_transaction_type='T',
			:gi:a_cost_center,
			-- a_account_category=condition(:gi:a_account_code = '4100', '40', condition(:gi:a_account_code <= '1999', '10', '20')),
			a_account_category=isnull( (select :a:a_default_category from /apps/kardia/data/Kardia_DB/a_account/rows a where :a:a_ledger_number = :gi:a_ledger_number and :a:a_account_code = :gi:a_account_code), '40'),
			:gi:a_account_code,
			a_amount = $0 - sum(:gi:a_amount),
			a_posted = 1,
			a_modified = 0,
			a_corrected = 0,
			a_correcting = 0,
			a_reconciled = 0,
			a_postprocessed = 0,
			a_postprocess_type = 'XX',
			a_origin = 'CR',
			a_receipt_sent = 1,
			a_receipt_desired = 1,
			a_first_gift = 0, 
			-- a_sent_document_id = convert(string, :a_receipt_number),
			-- p_ext_partner_id = :p_donor_partner_id,
			-- :a_goods_provided,
			a_goods_provided = $0,
			s_date_created = getdate(),
			s_date_modified = getdate(),
			s_created_by = user_name(),
			s_modified_by = user_name(),
			a_comment = '' + count(:gi:a_amount) + ' Gifts for ' + :gi:a_cost_center
		FROM
			/apps/kardia/data/Kardia_DB/a_batch/rows b,
			/apps/kardia/data/Kardia_DB/a_subtrx_gift_item/rows gi
		WHERE
			:b:a_batch_number = :parameters:a_batch_number and
			:b:a_ledger_number = :parameters:a_ledger_number and
			:b:a_period = :parameters:a_period and
			:gi:a_ledger_number = :b:a_ledger_number and
			:gi:a_batch_number = :b:a_batch_number
		GROUP BY
			:b:a_ledger_number, :b:a_batch_number, :gi:a_cost_center, :gi:a_account_code
		;

		INSERT INTO
			/apps/kardia/data/Kardia_DB/a_transaction/rows
		SELECT
			:b:a_ledger_number,
			a_batch_number = :parameters:a_batch_number,
			a_journal_number=1,
			:b:a_period,
			-- a_effective_date = isnull(:a_gift_postmark_date, :a_gift_received_date),
			a_effective_date = :b:a_default_effective_date,
			a_transaction_type='T',
			a_cost_center = :parameters:a_cash_costctr,
			a_account_category=:config:cash_class,
			a_account_code = :parameters:a_cash_account,
			a_amount = sum(:gi:a_amount),
			a_posted = 1,
			a_modified = 0,
			a_corrected = 0,
			a_correcting = 0,
			a_reconciled = 0,
			a_postprocessed = 0,
			a_postprocess_type = 'XX',
			a_origin = 'CR',
			a_receipt_sent = 0,
			a_receipt_desired = 0,
			a_first_gift = 0,
			a_goods_provided = $0.00,
			s_date_created = getdate(),
			s_date_modified = getdate(),
			s_created_by = user_name(),
			s_modified_by = user_name(),
			a_comment = '' + count(:gi:a_amount) + ' Gifts deposited'
		FROM
			/apps/kardia/data/Kardia_DB/a_batch/rows b,
			/apps/kardia/data/Kardia_DB/a_subtrx_gift_item/rows gi
		WHERE
			:b:a_batch_number = :parameters:a_batch_number and
			:b:a_ledger_number = :parameters:a_ledger_number and
			:b:a_period = :parameters:a_period and
			:gi:a_ledger_number = :b:a_ledger_number and
			:gi:a_batch_number = :b:a_batch_number
		GROUP BY
			:b:a_ledger_number, :b:a_batch_number
		;

		UPDATE
			/apps/kardia/data/Kardia_DB/a_admin_fee_type/rows at
		SET
			:a_tmp_total_percentage = (SELECT isnull(sum(:a_percentage), 0.0)
						    FROM /apps/kardia/data/Kardia_DB/a_admin_fee_type_item/rows ai
						    WHERE :ai:a_admin_fee_type = :at:a_admin_fee_type and :ai:a_admin_fee_subtype = :at:a_admin_fee_subtype and :ai:a_ledger_number = :at:a_ledger_number),
			:a_tmp_fixed_percentage = (SELECT isnull(sum(:a_percentage), 0.0)
						    FROM /apps/kardia/data/Kardia_DB/a_admin_fee_type_item/rows ai
						    WHERE :ai:a_admin_fee_type = :at:a_admin_fee_type and :ai:a_admin_fee_subtype = :at:a_admin_fee_subtype and :ai:a_ledger_number = :at:a_ledger_number and :ai:a_is_fixed = 1)
		;

		UPDATE
			IDENTITY /apps/kardia/data/Kardia_DB/a_subtrx_gift_item/rows g,
			/apps/kardia/data/Kardia_DB/a_cc_admin_fee/rows af
		SET
			:g:a_calc_admin_fee_type = :af:a_admin_fee_type,
			:g:a_calc_admin_fee_subtype = isnull(:g:a_gift_admin_subtype, :af:a_default_subtype)
		WHERE
			:g:a_ledger_number = :parameters:a_ledger_number and
			:g:a_cost_center = :af:a_cost_center and
			:af:a_ledger_number = :parameters:a_ledger_number and
			:g:a_batch_number = :parameters:a_batch_number
		;

		UPDATE
			IDENTITY /apps/kardia/data/Kardia_DB/a_subtrx_gift_item/rows g,
			/apps/kardia/data/Kardia_DB/a_cc_admin_fee/rows af,
			/apps/kardia/data/Kardia_DB/a_admin_fee_type/rows at
		SET
			:g:a_calc_admin_fee = isnull(:g:a_gift_admin_fee, isnull(:af:a_percentage, isnull(:at:a_default_percentage, :at:a_tmp_total_percentage)))
		WHERE
			:g:a_ledger_number = :parameters:a_ledger_number and
			:g:a_cost_center = :af:a_cost_center and
			:af:a_ledger_number = :parameters:a_ledger_number and
			:at:a_ledger_number = :parameters:a_ledger_number and
			:at:a_admin_fee_type = :g:a_calc_admin_fee_type and
			:at:a_admin_fee_subtype = :g:a_calc_admin_fee_subtype and
			:g:a_batch_number = :parameters:a_batch_number
		;

		INSERT INTO
			/apps/kardia/data/Kardia_DB/a_transaction_tmp/rows
		SELECT
			:g:a_ledger_number,
			a_batch_number = :parameters:a_batch_number,
			a_journal_number=2,
			:g:a_period,
			-- a_effective_date=isnull(:g:a_gift_postmark_date, :g:a_gift_received_date),
			a_effective_date = :b:a_default_effective_date,
			a_transaction_type='T',
			a_cost_center=:g:a_cost_center,
			a_sent_document_id=:ai:a_dest_cost_center,
			a_account_category=:config:admin_exp_class,
			a_account_code=:config:admin_exp,
			a_amount=round(convert(money, condition(:ai:a_is_fixed == 1, condition(:g:a_calc_admin_fee >= :at:a_tmp_fixed_percentage, 1.0, condition(:g:a_calc_admin_fee == 0.0, 0.0, :g:a_calc_admin_fee / :at:a_tmp_fixed_percentage)), condition(:g:a_calc_admin_fee >= :at:a_tmp_fixed_percentage and (:at:a_tmp_total_percentage - :at:a_tmp_fixed_percentage) > 0.0, (:g:a_calc_admin_fee - :at:a_tmp_fixed_percentage) / (:at:a_tmp_total_percentage - :at:a_tmp_fixed_percentage), 0.0)) * :ai:a_percentage * sum(:g:a_amount)), 2),
			a_posted = 1,
			a_modified = 0,
			a_corrected = 0,
			a_correcting = 0,
			a_reconciled = 0,
			a_postprocessed = 0,
			a_postprocess_type = 'XX',
			a_origin = 'CR',
			a_receipt_sent = 0,
			a_receipt_desired = 0,
			a_first_gift = 0,
			-- a_sent_document_id = convert(string, :a_receipt_number),
			-- p_ext_partner_id = :p_donor_partner_id,
			a_goods_provided = sum(:g:a_amount),
			s_date_created = getdate(),
			s_date_modified = getdate(),
			s_created_by = user_name(),
			s_modified_by = user_name(),
			a_comment = :ai:a_comment
		FROM
			/apps/kardia/data/Kardia_DB/a_batch/rows b,
			/apps/kardia/data/Kardia_DB/a_subtrx_gift_item/rows g,
			/apps/kardia/data/Kardia_DB/a_admin_fee_type/rows at,
			/apps/kardia/data/Kardia_DB/a_admin_fee_type_item/rows ai
		WHERE
			:b:a_batch_number = :parameters:a_batch_number and
			:b:a_ledger_number = :parameters:a_ledger_number and
			:b:a_period = :parameters:a_period and
			:g:a_ledger_number = :b:a_ledger_number and
			:g:a_batch_number = :b:a_batch_number and
			:at:a_admin_fee_type = :g:a_calc_admin_fee_type and
			:at:a_admin_fee_subtype = :g:a_calc_admin_fee_subtype and
			:ai:a_ledger_number = :g:a_ledger_number and
			:ai:a_admin_fee_type = :g:a_calc_admin_fee_type and
			:ai:a_admin_fee_subtype = :g:a_calc_admin_fee_subtype
		GROUP BY
			:b:a_ledger_number,
			:b:a_batch_number,
			:g:a_cost_center,
			:g:a_calc_admin_fee_subtype,
			:g:a_calc_admin_fee,
			:ai:a_dest_cost_center
		HAVING
			:a_amount != $0.00
		;

		INSERT INTO
			/apps/kardia/data/Kardia_DB/a_transaction/rows
		SELECT
			:a_ledger_number,
			:a_batch_number,
			:a_journal_number,
			:a_period,
			:a_effective_date,
			:a_transaction_type,
			a_posted=1,
			a_modified=0,
			a_corrected=0,
			a_correcting=0,
			a_reconciled=0,
			a_postprocessed=0,
			a_postprocess_type = 'XX',
			a_origin = 'CR',
			a_receipt_sent = 0,
			a_receipt_desired = 0,
			a_first_gift = 0,
			a_goods_provided = $0,
			s_date_created = getdate(),
			s_date_modified = getdate(),
			s_created_by = user_name(),
			s_modified_by = user_name(),
			a_comment = 'Admin fee to ' + :a_sent_document_id + ' for gift(s) totaling ' + sum(:a_goods_provided) + isnull(' (' + :a_comment + ')', ''),
			:a_account_code,
			:a_account_category,
			a_amount = sum(:a_amount),
			:a_cost_center
		FROM
			/apps/kardia/data/Kardia_DB/a_transaction_tmp/rows t
		WHERE
			:t:a_batch_number = :parameters:a_batch_number and
			:t:a_origin = 'CR' and
			:t:a_ledger_number = :parameters:a_ledger_number and
			:t:a_period = :parameters:a_period and
			:t:a_journal_number = 2
		GROUP BY
			:a_sent_document_id, :a_cost_center
		;

		INSERT INTO
			/apps/kardia/data/Kardia_DB/a_transaction/rows
		SELECT
			:a_ledger_number,
			:a_batch_number,
			:a_journal_number,
			:a_period,
			:a_effective_date,
			:a_transaction_type,
			a_posted=1,
			a_modified=0,
			a_corrected=0,
			a_correcting=0,
			a_reconciled=0,
			a_postprocessed=0,
			a_postprocess_type = 'XX',
			a_origin = 'CR',
			a_receipt_sent = 0,
			a_receipt_desired = 0,
			a_first_gift = 0,
			a_goods_provided = $0,
			s_date_created = getdate(),
			s_date_modified = getdate(),
			s_created_by = user_name(),
			s_modified_by = user_name(),
			a_comment = 'Admin fee revenue',
			a_account_code = :config:admin_rev,
			a_account_category = :config:admin_rev_class,
			a_amount = $0 - sum(:a_amount),
			a_cost_center = :a_sent_document_id
		FROM
			/apps/kardia/data/Kardia_DB/a_transaction_tmp/rows t
		WHERE
			:t:a_batch_number = :parameters:a_batch_number and
			:t:a_origin = 'CR' and
			:t:a_ledger_number = :parameters:a_ledger_number and
			:t:a_period = :parameters:a_period and
			:t:a_journal_number = 2
		GROUP BY
			:a_sent_document_id
		;

		INSERT INTO
			/apps/kardia/data/Kardia_DB/a_transaction/rows
		SELECT
			a_amount = $0 - sum(:t:a_amount),
			a_comment = '' + count(1) + ' transactions for ' + :c:a_bal_cost_center,
			a_account_code = :config:ifa_acct,
			a_cost_center=:c:a_bal_cost_center,
			:t:a_journal_number,
			:t:a_batch_number,
			:t:a_ledger_number,
			:t:a_period,
			:t:a_effective_date,
			:t:a_transaction_type,
			a_account_category = :config:ifa_class,
			a_posted = 1,
			a_modified = 0,
			a_corrected = 0,
			:t:a_correcting,
			:t:a_corrected_batch,
			:t:a_corrected_journal,
			:t:a_corrected_transaction,
			:t:a_reconciled,
			:t:a_postprocessed,
			:t:a_postprocess_type,
			:t:a_origin,
			a_receipt_sent = 0,
			a_receipt_desired = 0,
			a_first_gift = 0,
			a_goods_provided = $0,
			s_date_created = getdate(),
			s_created_by = user_name(),
			s_date_modified = getdate(),
			s_modified_by = user_name()
		FROM
			/apps/kardia/data/Kardia_DB/a_transaction/rows t,
			/apps/kardia/data/Kardia_DB/a_cost_center/rows c
		WHERE
			:t:a_ledger_number = :c:a_ledger_number and
			:t:a_cost_center = :c:a_cost_center and
			:t:a_ledger_number = :parameters:a_ledger_number and
			:t:a_period = :parameters:a_period and
			:t:a_batch_number = :parameters:a_batch_number
		GROUP BY
			:t:a_ledger_number,
			:t:a_batch_number,
			:t:a_journal_number,
			:c:a_bal_cost_center
		ORDER BY
			:t:a_cost_center
		HAVING
			:a_amount != $0
		;
		UPDATE
			/apps/kardia/data/Kardia_DB/a_subtrx_gift_item/rows g
		SET
			:g:a_posted_to_gl = 1
		WHERE
			:g:a_batch_number = :parameters:a_batch_number and
			:g:a_ledger_number = :parameters:a_ledger_number and
			:g:a_period = :parameters:a_period
		;
		UPDATE
			/apps/kardia/data/Kardia_DB/a_subtrx_gift_group/rows g
		SET
			:g:a_posted_to_gl = 1
		WHERE
			:g:a_batch_number = :parameters:a_batch_number and
			:g:a_ledger_number = :parameters:a_ledger_number and
			:g:a_period = :parameters:a_period
		";

	end_glpost "widget/connector"
	    { 
	    event=EndQuery;
	    target=post_cr_batch;
	    action=GLPosted;
	    a_batch_number = runclient(:glpost_osrc_1:a_batch_number);
	    a_ledger_number = runclient(:glpost_osrc_1:a_ledger_number);
	    a_period = runclient(:glpost_osrc_1:a_period); 
	    }
	}
    }
